From zsh-workers-return-22778-mason-zsh=primenet.com.au@sunsite.dk Wed Sep 27 14:42:52 2006
Return-Path: <zsh-workers-return-22778-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 25688 invoked from network); 27 Sep 2006 14:42:46 -0000
X-Spam-Checker-Version: SpamAssassin 3.1.5 (2006-08-29) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.5 required=5.0 tests=BAYES_00,FORGED_RCVD_HELO 
	autolearn=ham version=3.1.5
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 27 Sep 2006 14:42:46 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 72351 invoked from network); 27 Sep 2006 14:42:39 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 27 Sep 2006 14:42:39 -0000
Received: (qmail 26827 invoked by alias); 27 Sep 2006 14:42:36 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 22778
Received: (qmail 26818 invoked from network); 27 Sep 2006 14:42:36 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 27 Sep 2006 14:42:36 -0000
Received: (qmail 72051 invoked from network); 27 Sep 2006 14:42:36 -0000
Received: from cluster-d.mailcontrol.com (217.69.20.190)
  by a.mx.sunsite.dk with SMTP; 27 Sep 2006 14:42:30 -0000
Received: from dalamsexb01.AMERICAS.ROOT.PRI ([65.208.56.47])
	by rly35d.srv.mailcontrol.com (MailControl) with ESMTP id k8REP5hB024438
	for <zsh-workers@sunsite.dk>; Wed, 27 Sep 2006 15:41:25 +0100
Received: from cameurexb01.EUROPE.ROOT.PRI ([10.100.137.61]) by dalamsexb01.AMERICAS.ROOT.PRI with Microsoft SMTPSVC(6.0.3790.1830);
	 Wed, 27 Sep 2006 09:21:51 -0500
Received: from news01.csr.com ([10.103.143.38]) by cameurexb01.EUROPE.ROOT.PRI with Microsoft SMTPSVC(6.0.3790.1830);
	 Wed, 27 Sep 2006 14:56:23 +0100
Received: from news01.csr.com (localhost.localdomain [127.0.0.1])
	by news01.csr.com (8.13.7/8.13.4) with ESMTP id k8RDuNZj025576
	for <zsh-workers@sunsite.dk>; Wed, 27 Sep 2006 14:56:23 +0100
Received: from csr.com (pws@localhost)
	by news01.csr.com (8.13.7/8.13.7/Submit) with ESMTP id k8RDuNZ5025572
	for <zsh-workers@sunsite.dk>; Wed, 27 Sep 2006 14:56:23 +0100
Message-Id: <200609271356.k8RDuNZ5025572@news01.csr.com>
X-Authentication-Warning: news01.csr.com: pws owned process doing -bs
To: zsh-workers@sunsite.dk (Zsh hackers list)
Subject: PATCH: completion for locate.
Date: Wed, 27 Sep 2006 14:56:23 +0100
From: Peter Stephenson <pws@csr.com>
X-OriginalArrivalTime: 27 Sep 2006 13:56:23.0954 (UTC) FILETIME=[B7FFAF20:01C6E23C]
Content-Type: text/plain
MIME-Version: 1.0
X-Scanned-By: MailControl A-07-04-02 (www.mailcontrol.com) on 10.68.0.145

Completion for GNU locate, slocate, mlocate, for options since there's
no way of completing patterns.  Decides the type; GNU locate isn't
properly tested but they're all pretty similar.  Currently misses some
tricks with paths as noted; also doesn't handle updatedb, except in the
case of slocate where that's built in.

Index: Completion/Unix/Command/_locate
===================================================================
RCS file: Completion/Unix/Command/_locate
diff -N Completion/Unix/Command/_locate
--- /dev/null	1 Jan 1970 00:00:00 -0000
+++ Completion/Unix/Command/_locate	27 Sep 2006 13:55:39 -0000
@@ -0,0 +1,111 @@
+#compdef locate mlocate slocate
+
+# Decide if we are using mlocate or slocate.
+local ltype basename=${words[0]:t} input
+# If we can't, use this guess.
+local best_guess=mlocate
+
+case $basename in
+  ([ms]locate)
+  ltype=$basename
+  ;;
+
+  (locate)
+  input="$(_call_program locate $words[0] -V)"
+  case $input in
+    (*mlocate*)
+    ltype=mlocate
+    ;;
+
+    (*(#i)secure locate*)
+    ltype=slocate
+    ;;
+
+    (*(#i)gnu locate*)
+    ltype=gnu
+    ;;
+
+    # guess
+    (*)
+    ltype=$best_guess
+    ;;
+  esac
+  ;;
+
+  (*)
+  # too dangerous to run: guess
+  ltype=$best_guess
+esac
+
+case $ltype in
+  (mlocate)
+  # actually, -d can take a colon-separate list
+  # -r/--regexp mean no normal arguments, so shouldn't complete
+  # -m and --mmap are ignored, so don't bother
+  # -s and --stdio likewise
+  _arguments -s : \
+    {-b,--basename}'[Match only the basename of files in the database]' \
+    {-c,--count}'[Output the number of matching entries]' \
+    {-d,--database=}'[Use alternative database]:database:_files' \
+    {-e,--existing}'[Restrict display to existing files]' \
+    {-L,--follow}'[Follow symbolic links to find existing files (default)]' \
+    {-h,--help}'[Show help]' \
+    {-i,--ignore-case}'[Ignore case distinctions in patterns]' \
+    {-l,-n,--limit=}'[Limit search results]:file limit: ' \
+    {-P,-H,--nofollow}'[Don'\''t follow symbolic links]' \
+    {-0,--null}'[Output separated by NUL characters]' \
+    {-S,--statistics}'[Show database statistics]' \
+    {-q,--quiet}'[Don'\''t report errors]' \
+    {-r,--regexp=}'[Search for given basic regexp]:basic regexp: ' \
+    --regex'[Patterns are extended regexps]' \
+    {-V,--version}'[Show version]' \
+    {-w,--wholename}'[Match entire file path (default)]' \
+    '*:pattern: '
+  ;;
+
+  (slocate)
+  # -d can take path
+  # -e can take a comma-separated list of directories.
+  # -f should complete list of file system types like mount
+  _arguments -s : \
+    -u'[Create slocate database starting at path /]' \
+    -U'[Create slocate database starting at given path]:directory:_files -/' \
+    -c'[Parse GNU locate updatedb with -u, -U]' \
+    -e'[Exclude directories with -u, -U]:directories:_files -/' \
+    -f'[Exclude file system types from db with -u, -U]:file system:_file_systems' \
+    -l'[Security level]:level:(0 1)' \
+    -q'[Quiet mode]' \
+    -n'[Limit search results]:file limit: ' \
+    -i'[Case insensitive search]' \
+    {-r,--regexp=}'[Use basic regular expression]:regexp: ' \
+    {-o,--output=}'[Specify database to create]:database:_files' \
+    {-d,--database=}'[Specify database to search]:database:_files' \
+    {-h,--help}'[Display help]' \
+    {-v,--verbose}'[Display files when creating database]' \
+    {-V,--version}'[Display version]' \
+    '*:pattern: '
+  ;;
+
+  (gnu)
+  _arguments -s : \
+    {-d,--database=}'[Use alternative database]:database:_files' \
+    {-e,--existing}'[Restrict display to existing files]' \
+    {-E,--non-existing}'[Allow display of nonexistent files (default)]' \
+    {-i,--ignore-case}'[Ignore case distinctions in patterns]' \
+    {-w,--wholename}'[Match entire file path (default)]' \
+    {-b,--basename}'[Match only the basename of files in the database]' \
+    {-l,-n,--limit=}'[Limit search results]:file limit: ' \
+    {-S,--statistics}'[Show database statistics]' \
+    {-0,--null}'[Output separated by NUL characters]' \
+    {-c,--count}'[Output the number of matching entries]' \
+    {-P,-H,--nofollow}'[Don'\''t follow symbolic links]' \
+    {-L,--follow}'[Follow symbolic links to find existing files (default)]' \
+    {-A,-all}'[Match all arguments instead of at least one]' \
+    {-p,--print}'[Include search results with statistics or count]' \
+    {-r,--regex=}'[Patterns are regular expressions]:basic regexp: ' \
+    --regextype='[Select type of regular expression]' \
+    {-V,--version}'[Show version]' \
+    --help'[Show help]' \
+    '*:pattern: '
+  ;;
+esac

-- 
Peter Stephenson <pws@csr.com>                  Software Engineer
CSR PLC, Churchill House, Cambridge Business Park, Cowley Road
Cambridge, CB4 0WZ, UK                          Tel: +44 (0)1223 692070


To access the latest news from CSR copy this link into a web browser:  http://www.csr.com/email_sig.php

