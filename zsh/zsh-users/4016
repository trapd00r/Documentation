From zsh-users-return-4016-mason-zsh=primenet.com.au@sunsite.dk Tue Jul 10 08:10:15 2001
Return-Path: <zsh-users-return-4016-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 18829 invoked from network); 10 Jul 2001 08:10:11 -0000
Received: from sunsite.dk (130.225.51.30)
  by ns1.primenet.com.au with SMTP; 10 Jul 2001 08:10:11 -0000
Received: (qmail 26672 invoked by alias); 10 Jul 2001 08:09:41 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 4016
Received: (qmail 26660 invoked from network); 10 Jul 2001 08:09:40 -0000
From: Sven Wischnowsky <wischnow@informatik.hu-berlin.de>
Date: Tue, 10 Jul 2001 10:07:18 +0200 (MET DST)
Message-Id: <200107100807.KAA14399@beta.informatik.hu-berlin.de>
To: zsh-users@sunsite.dk
Subject: Re: zsh 4.0.1 _man bug
In-Reply-To: <1010710074900.ZM28083@candle.brasslantern.com>

Bart Schaefer wrote:

> On Jul 10,  9:04am, Max Ischenko wrote:
> }
> } man n listb<TAB> -> man n listbox.n
> } 
> } Does anyone have a fix for this?
> 
> Try 4.0.2 ... the _man completion function was significantly improved.

Actually, it had the same bug, which is fixed in the new _man function
(included below for your convenience, it should work with 4.0.1, too).


Bye
  Sven

#compdef man apropos whatis

_man() {
  local dirs expl mrd awk

  if [[ $service == man ]] && (( $words[(I)-l] + $words[(I)--local-file] )); then
    _files || return 0
  fi

  if (( ! $#manpath )); then
    local mp
    mp=($(manpath 2>/dev/null))
    [[ "$mp" == *:* ]] && mp=( ${(s.:.)mp} )
    manpath=( $mp )
  fi

  (( $#manpath )) || manpath=( ${(s.:.)$(manpath 2>/dev/null)} ) ||
      manpath=( /usr/man(-/) /(opt|usr)/(dt|share|X11R6|local)/(cat|)man(-/) )

  # `sman' is the SGML manual directory for Solaris 7.
  # 1M is system administrator commands on SVR4

  mrd=(${^manpath/\%L/${LANG:-En_US.ASCII}}/mandb(N))

  if [[ $words[2] = (<->*|1M|l|n) ]]; then
    dirs=( $^manpath/(sman|man|cat)${words[2]}/ )
    awk="\$2 == \"$words[2]\" {print \$1}"
  else
    dirs=( $^manpath/(sman|man|cat)*/ )
    awk='{print $1}'
  fi

  _wanted manuals expl 'manual page' _man_pages
}

_man_pages() {
  local matcher pages dummy

  zparseopts -E M+:=matcher

  if (( $#matcher )); then
    matcher=( ${matcher:#-M} )
    matcher="$matcher"
  else
    matcher=
  fi

  pages=( $dirs )
  compfiles -p pages '' '' "$matcher" '' dummy '*'
  pages=( ${^~pages}(N:t:r) )

  (($#mrd)) && pages[$#pages+1]=($(awk $awk $mrd))

  # Remove any compression suffix, then remove the minimum possible string
  # beginning with .<->: that handles problem cases like files called
  # `POSIX.1.5'.

  compadd "$@" - ${pages%.(?|<->*)}
}

_man "$@"

-- 
Sven Wischnowsky                         wischnow@informatik.hu-berlin.de

