From zsh-workers-return-18863-mason-zsh=primenet.com.au@sunsite.dk Thu Jul 17 04:19:33 2003
Return-Path: <zsh-workers-return-18863-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 940 invoked from network); 17 Jul 2003 04:19:32 -0000
Received: from sunsite.dk (130.225.247.90)
  by ns1.primenet.com.au with SMTP; 17 Jul 2003 04:19:32 -0000
Received: (qmail 367 invoked by alias); 17 Jul 2003 04:19:27 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 18863
Received: (qmail 357 invoked from network); 17 Jul 2003 04:19:26 -0000
Received: from localhost (HELO sunsite.dk) (127.0.0.1)
  by localhost with SMTP; 17 Jul 2003 04:19:26 -0000
X-MessageWall-Score: 0 (sunsite.dk)
Received: from [203.109.249.144] by sunsite.dk (MessageWall 1.0.8) with SMTP; 17 Jul 2003 4:19:26 -0000
Received: from p275-tnt2.syd.ihug.com.au (localhost.localdomain) [203.173.131.21] 
	by grunt24.ihug.com.au with esmtp (Exim 3.35 #1 (Debian))
	id 19d0El-0008Qp-00; Thu, 17 Jul 2003 14:19:24 +1000
Received: from localhost.localdomain (localhost.localdomain [127.0.0.1])
	by localhost.localdomain (8.12.5/8.12.5) with ESMTP id h6H4XpRk007126
	for <zsh-workers@sunsite.dk>; Thu, 17 Jul 2003 14:33:51 +1000
Received: (from doug@localhost)
	by localhost.localdomain (8.12.5/8.12.5/Submit) id h6H4XnOM007124
	for zsh-workers@sunsite.dk; Thu, 17 Jul 2003 14:33:49 +1000
X-Authentication-Warning: localhost.localdomain: doug set sender to djkea2@mugca.its.monash.edu.au using -f
Date: Thu, 17 Jul 2003 14:33:49 +1000
From: Doug Kearns <djkea2@mugca.its.monash.edu.au>
To: zsh-workers@sunsite.dk
Subject: PATCH: aap completion
Message-ID: <20030717043349.GF821@localhost.localdomain>
Mail-Followup-To: zsh-workers@sunsite.dk
Mime-Version: 1.0
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline
User-Agent: Mutt/1.4i

Here's a first cut at a completion function for aap.

http://www.a-a-p.org/

Regards,
Doug

Index: Completion/Unix/Command/_aap
===================================================================
RCS file: Completion/Unix/Command/_aap
diff -N Completion/Unix/Command/_aap
--- /dev/null	1 Jan 1970 00:00:00 -0000
+++ Completion/Unix/Command/_aap	17 Jul 2003 04:17:24 -0000
@@ -0,0 +1,45 @@
+#compdef aap
+
+# A-A-P recipe executive version 1.001; released 2003 Jul 05 20:44:44 GMT
+
+local curcontext="$curcontext" state line
+typeset -A opt_args
+
+_arguments \
+  '(- *)'{-V,--version}'[print version information and exit]' \
+  '(- *)'{-h,--help}'[print help message (this one) and exit]' \
+  {-v,--verbose}'[print more information]' \
+  {-s,--silent}'[print less information]' \
+  {-d,--debug=}'[debug the specified items]:flags:' \
+  '--profile=[profile A-A-P execution and write results in FILE]:output file:_files' \
+  {-n,--nobuild}'[print the build commands but do not execute them]' \
+  '--changed=[consider FILE changed]:changed file:_files' \
+  {-t,--touch}'[update target signatures, do not build]' \
+  {-F,--force}'[force rebuilding]' \
+  {-C,--contents}'[only build when file contents changed]' \
+  \*{-c,--command=}'[execute a command after reading the recipe]:aap command:' \
+  {-k,--continue}'[continue building after an error]' \
+  {-S,--stop}'[stop building at first error (default)]' \
+  {-R,--fetch-recipe}'[fetch recipe file and child recipes]' \
+  {-N,--nofetch-recipe}'[do not fetch recipes for "fetch" target]' \
+  {-a,--nocache}"[always download files, don't use the cache]" \
+  {-l,--local}'[do not recurse into subdirectories]' \
+  {-j,--jobs=}'[maximum number of parallel jobs]:number of jobs:' \
+  {-u,--search-up,--up}'[search directory tree upwards for main.aap recipe]' \
+  \*{-I,--include=}'[directory to search for included recipes]:include directory:_files -/' \
+  {-f,--recipe=}'[recipe file to be executed]:recipe file:_files -g \*.aap' \
+  '--install=[install package NAME]:package name:' \
+  '--[end of options, targets and assignments follow]' \
+  '*:aap target:->target' && return
+
+if [[ "$state" = target ]]; then
+  local aap_targets aap_recipe expl
+
+  aap_recipe=${(v)opt_args[(I)(-f|--recipe)]:-main.aap}
+  if [[ -f $aap_recipe ]] && _tags targets; then
+    aap_targets=( "${(@)${(@)${(@f)$( _call_program targets "$words[1]" -f $aap_recipe comment 2>/dev/null )}#target }%%: *}" comment )
+    _wanted targets expl target compadd -a aap_targets && return
+  fi
+fi
+
+return 1

