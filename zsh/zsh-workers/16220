From zsh-workers-return-16220-mason-zsh=primenet.com.au@sunsite.dk Sat Nov 03 21:59:55 2001
Return-Path: <zsh-workers-return-16220-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 15313 invoked from network); 3 Nov 2001 21:59:55 -0000
Received: from ns2.primenet.com.au (HELO primenet.com.au) (?zLM27JMltDQ8awHBmJbbSGbH0Wb4VOix?@203.24.36.3)
  by ns1.primenet.com.au with SMTP; 3 Nov 2001 21:59:55 -0000
Received: (qmail 17811 invoked from network); 3 Nov 2001 21:59:53 -0000
Received: from sunsite.dk (130.225.247.90)
  by proxy.melb.primenet.com.au with SMTP; 3 Nov 2001 21:59:53 -0000
Received: (qmail 21935 invoked by alias); 3 Nov 2001 21:59:47 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 16220
Received: (qmail 21922 invoked from network); 3 Nov 2001 21:59:47 -0000
From: Borsenkow Andrej <Andrej.Borsenkow@mow.siemens.ru>
To: ZSH Workers Mailing List <zsh-workers@sunsite.dk>
Message-ID: <3BE468BE.8060900@mow.siemens.ru>
Date: Sun, 04 Nov 2001 00:59:26 +0300
User-Agent: Mozilla/5.0 (X11; U; Linux i686; en-US; rv:0.9.5) Gecko/20011012
X-Accept-Language: en-us
MIME-Version: 1.0
Subject: Mandrake urpmi suite completion
References: <3BE45087.707@mow.siemens.ru> <1011103202735.ZM8656@candle.brasslantern.com>
Content-Type: multipart/mixed;
 boundary="------------060106090800050404080506"

This is a multi-part message in MIME format.
--------------060106090800050404080506
Content-Type: text/plain; charset=KOI8-R; format=flowed
Content-Transfer-Encoding: 7bit

Bart Schaefer wrote:

> Unless I'm missing something:
> 
> 	elif compset -P removable_; then
> 	    compadd -S '' -- cdrom_ floppy_ && return 0
> 
> 



Really :)

O.K. attached is completion for (part of) urpmi programs on Mandrake. 
urpmi is similar to apt on Debian - you can define install media 
(local/removable/ftp/http) and have a list of all available packages 
with automatic dependency resolution. It does not include completion for 
urpmf or urpmq because I do not quite understand what they are doing 
besides teh simplest usage.

Should it go into Completion/Mandrake?

Suggestions for it are welcome. Chmouel, what about adding it to Zsh 
RPM? We need update for 4.0.4 anyway.

-andrej





--------------060106090800050404080506
Content-Type: text/plain;
 name="_urpmi"
Content-Transfer-Encoding: 7bit
Content-Disposition: inline;
 filename="_urpmi"

#compdef urpmi urpmi.addmedia urpmi.removemedia urpmi.update
local state context line
typeset -A opt_args

_urpmi_cache_policy() {
    [[ -e "$1" && -e /var/lib/urpmi/depslist.ordered && \
	"$1" -nt /var/lib/urpmi/depslist.ordered ]] && return 1
    return 0
}
    
case "$service" in
    urpmi.addmedia )
	_arguments -A '-*' \
	    "-update[mark as update media]" \
	    ":name of media: " \
	    ":media URL:->media_url" \
	    ": :(with)" \
	    ":relative path to hdlist file: " \
	 && return 0
    ;;
    urpmi.removemedia )
	_arguments -A '-*' \
	    "(:)-a[select all media]" \
	    "(-a)"{,\*}": :->urpmi_media" \
	 && return 0
    ;;
    urpmi.update )
	_arguments -A '-*' \
	    "(:)-a[select all non-removable media]" \
	    "-c[clean /var/cache/urpmi/headers on exit]" \
	    "*-f[force rebuild of hdlist or base files (if repeated)]" \
	    "(-a)"{,\*}": :->urpmi_media" \
	 && return 0
    ;;
    urpmi )
	_arguments -A '-*' \
	    "(: -)--help[print usage information]" \
	    "(--help)--update[use only update media]" \
	    "(--help)--auto[do not ask any questions]" \
	    "(--help)--auto-select[select the pakages to update]" \
	    "(--help)--force[preceed even when some packages do not exist]" \
	    "(--help)--best-output[automatically select text or X interface]" \
	    "(--help)-a[select all packages matching command line]" \
	    "(--help -m -M)-m[choose minimum closure of requires (default)]" \
	    "(--help -m -M)-M[choose maximum closure of requires]" \
	    "(--help)-c[choose complete method for resolving requires]" \
	    "(--help)-p[allow search in provides]" \
	    "(--help -q -v)-q[be quiet]" \
	    "(--help -q -v)-v[verbose mode]" \
	    "(--help)"{,\*}": :->urpmi_rpms" \
	&& return 0
    ;;
esac

case "$state" in
    media_url )
	if compset -P file://; then
	    _files -W / -/ && return 0
	elif compset -P removable_cdrom_\?://; then
	    _files -/ && return 0
	elif compset -P removable_; then
	    compadd -S "" -- cdrom_ floppy_ && return 0
	elif [[ -prefix '(ftp|http):' ]]; then
	    _url && return 0
	else
	    compadd -- file:// http:// ftp:// removable_
	fi
    ;;
    urpmi_media )
	local source media brace ret=1
	while read source media brace; do
	    [[ "$brace" != "{" ]] && continue
	    _wanted urpmi_media expl 'available media' \
		compadd -- "$source"
	    ret=0
	done < /etc/urpmi/urpmi.cfg
	return "$ret"
    ;;
    urpmi_rpms )
	local pkg foo expl
	local -a pkgs
	if [[ -r /var/lib/urpmi/depslist.ordered ]]; then
	    if _cache_invalid _urpmi_rpms || ! _retrieve_cache _urpmi_rpms; then
		while read pkg foo; do
		    [[ "$pkg" == (#b)(*)-[^-]##-[^-]## ]] && {
			pkgs[$#pkgs+1]=("$match[1]")
		    }
		done < /var/lib/urpmi/depslist.ordered
		_store_cache _urpmi_rpms pkgs
	    fi
	    _wanted urpmi_rpms expl 'RPM to install' \
		compadd -a pkgs && return 0
	fi
    ;;
esac

return 1


--------------060106090800050404080506--

