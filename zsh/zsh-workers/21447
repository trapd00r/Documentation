From zsh-workers-return-21447-mason-zsh=primenet.com.au@sunsite.dk Tue Jul 12 12:25:17 2005
Return-Path: <zsh-workers-return-21447-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 16274 invoked from network); 12 Jul 2005 12:25:16 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 12 Jul 2005 12:25:16 -0000
Received: (qmail 61498 invoked from network); 12 Jul 2005 12:25:08 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 12 Jul 2005 12:25:08 -0000
Received: (qmail 11843 invoked by alias); 12 Jul 2005 12:25:04 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 21447
Received: (qmail 11832 invoked from network); 12 Jul 2005 12:25:03 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 12 Jul 2005 12:25:03 -0000
Received: (qmail 61045 invoked from network); 12 Jul 2005 12:25:03 -0000
Received: from acolyte.scowler.net (216.254.112.45)
  by a.mx.sunsite.dk with SMTP; 12 Jul 2005 12:24:58 -0000
Received: by acolyte.scowler.net (Postfix, from userid 1000)
	id BB40E70057; Tue, 12 Jul 2005 08:24:56 -0400 (EDT)
Date: Tue, 12 Jul 2005 08:24:56 -0400
From: Clint Adams <clint@zsh.org>
To: zsh-workers@sunsite.dk
Cc: Emmanuel Hainry <Emmanuel.Hainry@loria.fr>,
	Emmanuel Beffara <manu@beffara.org>
Subject: PATCH: _rubber
Message-ID: <20050712122456.GA8278@scowler.net>
Mail-Followup-To: zsh-workers@sunsite.dk,
	Emmanuel Hainry <Emmanuel.Hainry@loria.fr>,
	Emmanuel Beffara <manu@beffara.org>
Mime-Version: 1.0
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline
User-Agent: Mutt/1.5.9i
X-Spam-Checker-Version: SpamAssassin 3.0.2 on a.mx.sunsite.dk
X-Spam-Level: 
X-Spam-Status: No, score=-2.6 required=6.0 tests=AWL,BAYES_00 autolearn=ham 
	version=3.0.2
X-Spam-Hits: -2.6

This is completion for rubber, contributed by Emmanuel
Hainry and Emmanuel Beffara.

Index: Completion/Unix/Command/_rubber
===================================================================
RCS file: Completion/Unix/Command/_rubber
diff -N Completion/Unix/Command/_rubber
--- /dev/null	1 Jan 1970 00:00:00 -0000
+++ Completion/Unix/Command/_rubber	12 Jul 2005 12:24:44 -0000
@@ -0,0 +1,70 @@
+#compdef rubber rubber-pipe rubber-info
+
+local _rubber_version _rubber_path _rubber_modules
+
+eval $(rubber --version | sed 's/^.* \([^ ]*\): */_rubber_\1=/')
+if [[ ${_rubber_version#0} != $_rubber_version ]]; then
+  _rubber_modules=(
+    $(ls $_rubber_path/rubber/modules | sed -n 's/^\([^_].*\)\.py$/\1/p')
+  )
+else
+  _rubber_modules=(
+    $(ls $_rubber_path/modules | sed -n 's/.rub$//p')
+    $(ls $_rubber_path/rubber/rules/latex | sed -n 's/^\([^_].*\)\.py$/\1/p')
+  )
+fi
+
+_rubber_arguments () {
+    _arguments -s \
+      \*{-c,--command}'=[run the directive CMD before parsing]:command' \
+      \*{-e,--epilogue}'=[run the directive CMD after parsing]:command' \
+      {-z,--gzip}'[compress the final document]' \
+      '(- *)'{-h,--help}'[show help]' \
+      '--into=[go to directory DIR before compiling]:directory:_files -/' \
+      {-l,--landscape}'[change paper orientation (if relevant)]' \
+      {-n,--maxerr}'=[display at most NUM errors]:num' \
+      \*{-m,--module}'=[use module]:module:($_rubber_modules)' \
+      '--only=[only include the specified SOURCES]:sources' \
+      \*{-o,--post}'=[postprocess with module]:postprocessor:($_rubber_modules)' \
+      {-d,--pdf}'[produce PDF output instead of DVI]' \
+      {-p,--ps}'[produce a PostScript document]' \
+      {-q,--quiet}'[suppress messages]' \
+      \*{-r,--read}'[read additional directives from a file]:directives files:_files' \
+      {-s,--short}'[display errors in a compact form]' \
+      \*{-I,--texpath}'=[add DIR to the search path for LaTeX]:tex path:_files -/' \
+      \*{-v,--verbose}'[increase verbosity]' \
+      '--version[print version information and exit]' "$@"
+}
+
+case "$service" in
+  rubber)
+    _rubber_arguments \
+      '--clean[remove produced files instead of compiling]' \
+      {-f,--force}'[force at least one compilation]' \
+      '--inplace[compile the documents from their source directory]' \
+      \*{-W,--warn}'=[report warnings of the given TYPE]:warnings:(all boxes misc refs)' \
+      '*:LaTeX files:_files -g \*.\(tex\|dtx\|lhs\|w\)'
+      return 0
+	;;
+	
+  rubber-pipe)
+    _rubber_arguments \
+      {-k,--keep}'[keep the temporary files after compiling]' \
+      \*{-W,--warn}'=[report warnings of the given TYPE]:warnings:(all boxes misc refs)'
+      return 0
+	;;
+	
+  rubber-info)
+    _rubber_arguments \
+      '--boxes[report overfull and underfull boxes]' \
+      '--check[report errors or warnings default action]' \
+      '--deps[show the target file s dependencies]' \
+      '--errors[show all errors that occured during compilation]' \
+      '--refs[show the list of undefined references]' \
+      '--warnings[show all LaTeX warnings]' \
+      ':LaTeX file:_files -g \*.\(tex\|dtx\|lhs\|w\)'
+      return 0
+  ;;
+esac
+
+return 1

