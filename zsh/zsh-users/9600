From zsh-users-return-9600-mason-zsh=primenet.com.au@sunsite.dk Tue Nov 01 00:18:37 2005
Return-Path: <zsh-users-return-9600-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 19756 invoked from network); 1 Nov 2005 00:18:28 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 1 Nov 2005 00:18:28 -0000
Received: (qmail 8430 invoked from network); 1 Nov 2005 00:18:12 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 1 Nov 2005 00:18:12 -0000
Received: (qmail 7645 invoked by alias); 1 Nov 2005 00:18:05 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 9600
Received: (qmail 7631 invoked from network); 1 Nov 2005 00:18:04 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 1 Nov 2005 00:18:04 -0000
Received: (qmail 7343 invoked from network); 1 Nov 2005 00:18:04 -0000
Received: from ns9.hostinglmi.net (213.194.149.146)
  by a.mx.sunsite.dk with SMTP; 1 Nov 2005 00:18:03 -0000
Received: from 212.red-80-35-44.staticip.rima-tde.net ([80.35.44.212] helo=localhost)
	by ns9.hostinglmi.net with esmtpa (Exim 4.52)
	id 1EWjqk-0005oS-0T
	for zsh-users@sunsite.dk; Tue, 01 Nov 2005 01:18:02 +0100
Date: Tue, 1 Nov 2005 01:19:13 +0100
From: DervishD <zsh@dervishd.net>
To: Zsh Users <zsh-users@sunsite.dk>
Subject: My "pprint" function
Message-ID: <20051101001913.GA680@DervishD>
Mail-Followup-To: Zsh Users <zsh-users@sunsite.dk>
Mime-Version: 1.0
Content-Type: text/plain; charset=iso-8859-1
Content-Disposition: inline
Content-Transfer-Encoding: 8bit
User-Agent: Mutt/1.4.2.1i
Organization: DervishD
X-AntiAbuse: This header was added to track abuse, please include it with any abuse report
X-AntiAbuse: Primary Hostname - ns9.hostinglmi.net
X-AntiAbuse: Original Domain - sunsite.dk
X-AntiAbuse: Originator/Caller UID/GID - [47 12] / [47 12]
X-AntiAbuse: Sender Address Domain - dervishd.net
X-Source: 
X-Source-Args: 
X-Source-Dir: 
X-Spam-Checker-Version: SpamAssassin 3.0.4 (2005-06-05) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.6 required=5.0 tests=AWL,BAYES_00 autolearn=ham 
	version=3.0.4

    Hi all :)

    I'm posting this code here just in case it can be improved, or if
anybody is interested in this function. I use it a lot in my scripts.
I wrote that initially to learn a bit more about zsh, a long time
ago, and with time I've corrected the source and well, here it is.

# Some sample styles I use for my scripts

# Default "pprint" style
zstyle ":pprint:*" attr none
zstyle ":pprint:*" fgcolor default
zstyle ":pprint:*" bgcolor default
zstyle ":pprint:*" pad none
zstyle ":pprint:*" width ""
zstyle ":pprint:*" prestr ""
zstyle ":pprint:*" posstr ""
zstyle ":pprint:*" fillchar ""
zstyle ":pprint:*" newline yes
zstyle ":pprint:*" rawmode no

# "pprint" style for saying things catching attention
zstyle ":pprint:say:*" attr bold
zstyle ":pprint:say:*" fgcolor yellow

# "pprint" style for warnings
zstyle ":pprint:warning:*" fgcolor yellow
zstyle ":pprint:warning:*" prestr "* "

# "pprint" style for errors
zstyle ":pprint:error:*" attr bold
zstyle ":pprint:error:*" fgcolor red
zstyle ":pprint:error:*" prestr "*** "

# "pprint" style for debug strings
zstyle ":pprint:debug:*" prestr "! "
zstyle ":pprint:debug:*" fgcolor cyan
zstyle ":pprint:debug:*" newline yes
zstyle ":pprint:debug:*" rawmode yes

# "pprint" style for about-to-begin actions
zstyle ":pprint:action:*" attr bold
zstyle ":pprint:action:*" fgcolor blue
zstyle ":pprint:action:*" pad right
zstyle ":pprint:action:*" width '$COLUMNS-${#:-"[*]"}'
zstyle ":pprint:action:*" posstr "  "
zstyle ":pprint:action:*" newline no

# "pprint" style for status markers
zstyle ":pprint:status:*" prestr "[*]"
zstyle ":pprint:status:*" attr bold
zstyle ":pprint:status:success:*" fgcolor green
zstyle ":pprint:status:ignored:*" fgcolor black
zstyle ":pprint:status:failure:*" fgcolor red


# The usage is "pprint <style>"
function pprint() {

    emulate -LR zsh

    local _attr _fgcolor _bgcolor
    local _pad _width
    local _prestr _posstr _fillchar
    local _newline _rawmode
    
    for _style_element in attr fgcolor bgcolor\
                          pad width\
                          prestr posstr fillchar\
                          newline rawmode
    do
        eval zstyle -s ":pprint:$1:" $_style_element _$_style_element
    done
    
    ((#)) && shift

    # The "_width" element may depend on other variables, like $COLUMNS    
    eval "_width=\$(( $_width ))"
    
    #   If the length of the prestring and the poststring is greater
    # than the number of available columns, then get rid of both.
    [[ $_width -gt 0 && $(( $#_prestr + $#_posstr )) -ge $_width ]] && {
        _prestr=""
        _posstr=""
    }

    if [[ "$_rawmode" == "y" ]]
    then _rawmode="-r"
    else _rawmode=""
    fi
    
    [[ -t 1 ]] && print -n -- "$attr[$_attr]$fg[$_fgcolor]$bg[$_bgcolor]"
    
    # If the especified width is empty or 0, then we don't do truncation
    _string="$*"
    (( _width )) && {
        (( _width -= ( $#_prestr + $#_posstr ) ))
        case $_pad[1] in
        n)
            eval _string="\"\${\${(pr:$_width::$_fillchar[1]::\0:)_string}%\$'\0'*}\""
            ;;
        r|l)
            eval _string="\"\${($_pad[1]:$_width::$_fillchar[1]:)_string}\""
            ;;
        esac
    }
    print $_rawmode -n -- "$_prestr$_string$_posstr"

    [[ -t 1 ]] && print -n -- "$bg[default]$fg[default]$attr[none]"

    [[ "$_newline[1]" == "y" ]] && print

    return 0
}

    I hope the code is not criptic, I think it's pretty easy to
understand. I'm sure it can be improved and fixed, and any
suggestion and/or criticism is welcome. It depends on "attr", "fg"
and "bg" arrays for coloring, but I think that zsh comes with them :?

    Try this:

    pprint action "This could be a work in progress" ; pprint success

    (I have aliases:
    alias action='true; pprint "action"'
    alias success='pprint "success"; true'
    alias failure='pprint "failure"; false'
    ...
    This makes easier to use the pprint system)

    Thanks a lot in advance and enjoy prettyprinting ;)

    Raúl Núñez de Arenas Coronado

-- 
Linux Registered User 88736 | http://www.dervishd.net
http://www.pleyades.net & http://www.gotesdelluna.net
It's my PC and I'll cry if I want to...

