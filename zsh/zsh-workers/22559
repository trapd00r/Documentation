From zsh-workers-return-22559-mason-zsh=primenet.com.au@sunsite.dk Fri Jul 28 09:13:17 2006
Return-Path: <zsh-workers-return-22559-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 7195 invoked from network); 28 Jul 2006 09:13:13 -0000
X-Spam-Checker-Version: SpamAssassin 3.1.3 (2006-06-01) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.3 required=5.0 tests=AWL,BAYES_00,
	FORGED_RCVD_HELO,SUBJ_HAS_UNIQ_ID autolearn=ham version=3.1.3
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 28 Jul 2006 09:13:13 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 3955 invoked from network); 28 Jul 2006 09:13:07 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 28 Jul 2006 09:13:07 -0000
Received: (qmail 9477 invoked by alias); 28 Jul 2006 09:13:05 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 22559
Received: (qmail 9468 invoked from network); 28 Jul 2006 09:13:04 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 28 Jul 2006 09:13:03 -0000
Received: (qmail 3716 invoked from network); 28 Jul 2006 09:13:03 -0000
Received: from cluster-c.mailcontrol.com (168.143.177.190)
  by a.mx.sunsite.dk with SMTP; 28 Jul 2006 09:13:01 -0000
Received: from cameurexb01.EUROPE.ROOT.PRI ([62.189.241.200])
	by rly19c.srv.mailcontrol.com (MailControl) with ESMTP id k6S9AKBX029470
	for <zsh-workers@sunsite.dk>; Fri, 28 Jul 2006 10:12:53 +0100
Received: from news01.csr.com ([10.103.143.38]) by cameurexb01.EUROPE.ROOT.PRI with Microsoft SMTPSVC(6.0.3790.1830);
	 Fri, 28 Jul 2006 10:10:49 +0100
Received: from news01.csr.com (localhost.localdomain [127.0.0.1])
	by news01.csr.com (8.13.4/8.13.4) with ESMTP id k6S9AnL5018633
	for <zsh-workers@sunsite.dk>; Fri, 28 Jul 2006 10:10:49 +0100
Received: from csr.com (pws@localhost)
	by news01.csr.com (8.13.4/8.13.4/Submit) with ESMTP id k6S9AmW2018630
	for <zsh-workers@sunsite.dk>; Fri, 28 Jul 2006 10:10:49 +0100
Message-Id: <200607280910.k6S9AmW2018630@news01.csr.com>
X-Authentication-Warning: news01.csr.com: pws owned process doing -bs
To: zsh-workers@sunsite.dk (Zsh hackers list)
Subject: Re: Menu-driven version of history-beginning-search-backward 
In-reply-to: <060727212432.ZM4920@torch.brasslantern.com> 
References: <200607261638.k6QGcE7E010498@news01.csr.com> <dc507f4a0607270901r5a4c19f2n20a895b8a831ab3@mail.gmail.com> <060727212432.ZM4920@torch.brasslantern.com>
Date: Fri, 28 Jul 2006 10:10:48 +0100
From: Peter Stephenson <pws@csr.com>
X-OriginalArrivalTime: 28 Jul 2006 09:10:49.0772 (UTC) FILETIME=[B8083EC0:01C6B225]
Content-Type: text/plain
MIME-Version: 1.0
X-Scanned-By: MailControl A-07-00-10 (www.mailcontrol.com) on 10.67.0.129

Bart Schaefer wrote:
> On 7/26/06, Peter Stephenson <pws@csr.com> wrote:
> > I was too lazy to search the mailing list archive for anything like
> > this, but in any case it would be good to have something like it in
> > the distribution.
> 
> April 2004, zsh-users/7370.

(Moved to zsh-workers.)

Oh yes, that's a completion widget instead.  It looks like it doesn't do
the "-beginning" bit, but it does do menu selection which is hard from
outside completion.  There's probably room for both.  I think I'll stick
mine in Functions/Zle.

Index: Doc/Zsh/contrib.yo
===================================================================
RCS file: /cvsroot/zsh/zsh/Doc/Zsh/contrib.yo,v
retrieving revision 1.58
diff -u -r1.58 contrib.yo
--- Doc/Zsh/contrib.yo	19 Apr 2006 18:03:02 -0000	1.58
+++ Doc/Zsh/contrib.yo	28 Jul 2006 09:07:20 -0000
@@ -605,6 +605,29 @@
 bindkey '\e^P' history-beginning-search-backward-end
 bindkey '\e^N' history-beginning-search-forward-end)
 )
+tindex(history-beginning-search-menu)
+item(tt(history-beginning-search-menu))(
+This function implements yet another form of history searching.  The
+text before the cursor is used to select lines from the history,
+as for tt(history-beginning-search-backward) except that all matches are
+shown in a numbered menu.  Typing the appropriate digits inserts the
+full history line.  Note that leading zeroes must be typed (they are only
+shown when necessary for removing ambiguity).  The entire history is
+searched; there is no distinction between forwards and backwards.
+
+With a prefix argument, the search is not anchored to the start of
+the line; the string typed by the use may appear anywhere in the line
+in the history.
+
+If the widget name contains `tt(-end)' the cursor is moved to the end of
+the line inserted.  If the widget name contains `tt(-space)' any space
+in the text typed is treated as a wildcard and can match anything (hence
+a leading space is equivalent to giving a prefix argument).  Both
+forms can be combined, for example:
+
+example(zle -N history-beginning-search-menu-space-end \ 
+       history-beginning-search-menu)
+)
 tindex(history-pattern-search)
 tindex(history-pattern-search-backward)
 tindex(history-pattern-search-forward)
Index: Functions/Zle/history-beginning-search-menu
===================================================================
RCS file: Functions/Zle/history-beginning-search-menu
diff -N Functions/Zle/history-beginning-search-menu
--- /dev/null	1 Jan 1970 00:00:00 -0000
+++ Functions/Zle/history-beginning-search-menu	28 Jul 2006 09:07:20 -0000
@@ -0,0 +1,89 @@
+# Menu-driven alternative to history-beginning-search-backward.
+# As it uses a menu there is no sense of "forward" or "backward", however;
+# the entire history is searched.
+#
+# Configuration:
+#   autoload -U history-beginning-search-menu
+#   zle -N history-beginning-search-menu
+#   bindkey '\eP' history-beginning-search-menu
+#
+# Example:
+#   % /bin/su<ESC-P>
+#   Enter digit:
+#   1 /bin/su -c 'make install'            4 /bin/su - perforce
+#   2 /bin/su                              5 /bin/su -c
+#   3 /bin/su -c 'chown pws:pws **/*(u0)'
+#
+# Typing "1" expands the line to
+#   % /bin/su -c 'make install'
+#
+# With a prefix argument, the search is not anchored to the beginning,
+# so for example "/su" could expand to "p4 files //depot/support/..."
+#
+# If this is bound to a widget containing "-end", e.g.
+#   zle -N history-beginning-search-menu-end history-beginning-search-menu
+# then the cursor is put at the end of the line, else it is left
+# after the matched characters.
+#
+# If this is bound to a widget containing "-space", then any space in
+# the line so far is matched as a wildcard.  (This means putting a space
+# at the start of the line is equivalent to specifying a prefix
+# argument.)
+
+emulate -L zsh
+setopt extendedglob
+
+zmodload -i zsh/parameter
+
+local -aU matches
+local -a display
+
+local search=$LBUFFER
+
+if [[ $WIDGET = *-space* ]]; then
+  search=${search//(#m)[*?#<>]/\\$MATCH/}
+  search=${search// /*}
+fi
+
+if (( ${+NUMERIC} )); then
+  matches=(${(o)history[(R)*${search}*]})
+else
+  matches=(${(o)history[(R)${search}*]})
+fi
+
+# Filter out any match that's the same as the original.
+# Note this isn't a pattern this time.
+matches=(${matches:#${LBUFFER}})
+
+integer n=${#matches}
+integer width=${#n}
+
+(( n == 0 )) && return 1
+
+# Hey, this works...
+integer i
+display=(${matches/(#m)*/${(l.$width..0.):-$((++i))} $MATCH})
+zle -R "Enter digit${${width##1}:+s}:" $display
+
+local chars
+read -k$width chars
+
+if [[ $chars != [[:digit:]]## || $chars -eq 0 || $chars -gt $n ]]; then
+  return 1
+fi
+
+if [[ $WIDGET = *-end* ]]; then
+  LBUFFER=${matches[$chars]} RBUFFER=
+else
+  integer newcursor
+  if (( ${+NUMERIC} )); then
+    # Advance cursor so that it's still after the string typed
+    local -a match mbegin mend
+    if [[ $matches[$chars] = (#b)(*${LBUFFER})* ]]; then
+      newcursor=${#match[1]}
+    fi
+  fi
+
+  BUFFER=${matches[$chars]}
+  (( newcursor )) && CURSOR=$newcursor
+fi

-- 
Peter Stephenson <pws@csr.com>                  Software Engineer
CSR PLC, Churchill House, Cambridge Business Park, Cowley Road
Cambridge, CB4 0WZ, UK                          Tel: +44 (0)1223 692070


To access the latest news from CSR copy this link into a web browser:  http://www.csr.com/email_sig.php

