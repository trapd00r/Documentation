From zsh-workers-return-18254-mason-zsh=primenet.com.au@sunsite.dk Tue Feb 18 05:54:25 2003
Return-Path: <zsh-workers-return-18254-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 5776 invoked from network); 18 Feb 2003 05:54:24 -0000
Received: from sunsite.dk (130.225.247.90)
  by ns1.primenet.com.au with SMTP; 18 Feb 2003 05:54:24 -0000
Received: (qmail 20610 invoked by alias); 18 Feb 2003 05:54:14 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 18254
Received: (qmail 20603 invoked from network); 18 Feb 2003 05:54:13 -0000
Received: from localhost (HELO sunsite.dk) (127.0.0.1)
  by localhost with SMTP; 18 Feb 2003 05:54:13 -0000
X-MessageWall-Score: 0 (sunsite.dk)
Received: from [203.109.249.145] by sunsite.dk (MessageWall 1.0.8) with SMTP; 18 Feb 2003 5:54:12 -0000
Received: from p74-tnt6.syd.ihug.com.au (localhost.localdomain) [203.173.146.74] 
	by grunt25.ihug.com.au with esmtp (Exim 3.35 #1 (Debian))
	id 18l0hp-000382-00; Tue, 18 Feb 2003 16:54:13 +1100
Received: from localhost.localdomain (localhost.localdomain [127.0.0.1])
	by localhost.localdomain (8.12.5/8.12.5) with ESMTP id h1I6Ei9P022633
	for <zsh-workers@sunsite.dk>; Tue, 18 Feb 2003 17:14:45 +1100
Received: (from doug@localhost)
	by localhost.localdomain (8.12.5/8.12.5/Submit) id h1I6Eg84022631
	for zsh-workers@sunsite.dk; Tue, 18 Feb 2003 17:14:42 +1100
X-Authentication-Warning: localhost.localdomain: doug set sender to djkea2@mugca.its.monash.edu.au using -f
Date: Tue, 18 Feb 2003 17:14:42 +1100
From: Doug Kearns <djkea2@mugca.its.monash.edu.au>
To: zsh-workers@sunsite.dk
Subject: PATCH: xmllint completion
Message-ID: <20030218061442.GA17147@localhost.localdomain>
Mail-Followup-To: zsh-workers@sunsite.dk
Mime-Version: 1.0
Content-Type: multipart/mixed; boundary="45Z9DzgjV8m4Oswq"
Content-Disposition: inline
User-Agent: Mutt/1.4i


--45Z9DzgjV8m4Oswq
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline

Here's a new completion function for xmllint which comes with the
libxml2 library - http://xmlsoft.org/

Regards,
Doug

--45Z9DzgjV8m4Oswq
Content-Type: text/plain; charset=us-ascii
Content-Disposition: attachment; filename=_xmllint

#compdef xmllint

# xmllint: using libxml version 20503

local encoding
encoding=(
  UTF-8
  UTF-16
  ISO-10646-UCS-2
  ISO-10646-UCS-4
  ISO-8859-1
  ISO-8859-2
  ISO-8859-3
  ISO-8859-4
  ISO-8859-5
  ISO-8859-6
  ISO-8859-7
  ISO-8859-8
  ISO-8859-9
  ISO-2022-JP
  SHIFT_JIS
  EUC-JP
)

_arguments \
  '--version[display the version of the XML library used]' \
  '--debug[dump a debug tree of the in-memory document]' \
  '--shell[run a navigating shell]' \
  '--debugent[debug the entities defined in the document]' \
  '--copy[used to test the internal copy implementation]' \
  '--recover[output what was parsable on broken XML documents]' \
  '--noent[substitute entity references by their value]' \
  "(--output -o)--noout[don't output the result tree]" \
  '--htmlout[output results as HTML]' \
  '--nowrap[do not put HTML doc wrapper]' \
  '--valid[validate the document in addition to std well-formed check]' \
  '(--dtdvalid --relaxng)--postvalid[do a posteriori validation, i.e after parsing]' \
  '(--postvalid --relaxng)--dtdvalid[do a posteriori validation against a given DTD]:DTD:->url' \
  '--timing[print some timings]' \
  '(--noout --output -o)'{--output,-o}'[save to a given file]:output file:_files' \
  '--repeat[repeat 100 times, for timing or profiling]' \
  '--insert[ad-hoc test for valid insertions]' \
  '--compress[turn on gzip compression of output]' \
  '(--html)--sgml[use the DocBook SGML parser]' \
  '(--sgml)--html[use the HTML parser]' \
  '--push[use the push mode of the parser]' \
  '--memory[parse from memory]' \
  '--nowarning[do not emit warnings from parser/validator]' \
  '--noblanks[drop (ignorable?) blanks spaces]' \
  '--format[reformat/reindent the input]' \
  '--testIO[test user I/O support]' \
  '--encode[output in the given encoding]:encoding:(${encoding[@]})' \
  '(--nocatalogs)--catalogs[use SGML catalogs]' \
  '(--catalogs)--nocatalogs[deactivate all catalogs]' \
  '--auto[generate a small doc on the fly]' \
  '--xinclude[do XInclude processing]' \
  '--loaddtd[fetch external DTD]' \
  '--dtdattr[loaddtd + populate the tree with inherited attributes]' \
  '--dropdtd[remove the DOCTYPE of the input docs]' \
  '--stream[use the streaming interface to process very large files]' \
  '--chkregister[verify the node registration code]' \
  '(--dtdvalid --postvalid)--relaxng[do RelaxNG validation against the schema]:schema:->url' \
  '*:XML file:->url' && return

if [[ "$state" = url ]]; then
  _alternative \
    'files:file:_files' \
    'urls:url:_urls' && return
fi

return 1

--45Z9DzgjV8m4Oswq--

