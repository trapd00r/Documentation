From zsh-workers-return-7495-mason-zsh=primenet.com.au@sunsite.auc.dk Thu Aug 26 10:54:44 1999
Return-Path: <zsh-workers-return-7495-mason-zsh=primenet.com.au@sunsite.auc.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 27255 invoked from network); 26 Aug 1999 10:54:40 -0000
Received: from sunsite.auc.dk (130.225.51.30)
  by ns1.primenet.com.au with SMTP; 26 Aug 1999 10:54:40 -0000
Received: (qmail 4930 invoked by alias); 26 Aug 1999 10:54:32 -0000
Mailing-List: contact zsh-workers-help@sunsite.auc.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.auc.dk
X-Seq: 7495
Received: (qmail 4922 invoked from network); 26 Aug 1999 10:54:26 -0000
To: zsh-workers@sunsite.auc.dk
Subject: Re: PATCH: completion
References: <199908250824.KAA10555@beta.informatik.hu-berlin.de>
MIME-Version: 1.0 (generated by AKEMI 1.13.2 - =?ISO-2022-JP?B?Ig==?=
 =?ISO-2022-JP?B?GyRCQTA0Y0s8GyhCIg==?=)
Content-Type: text/plain; charset=US-ASCII
From: Tanaka Akira <akr@jaist.ac.jp>
Date: 26 Aug 1999 19:54:09 +0900
In-Reply-To: Sven Wischnowsky's message of "Wed, 25 Aug 1999 10:24:49 +0200 (MET DST)"
Message-ID: <rsq3dx6q0by.fsf@crane.jaist.ac.jp>
Lines: 765
User-Agent: Chao-gnus/6.12.5 AKEMI/1.13.2 (=?ISO-2022-JP?B?GyRCQTAbKEI=?=
 =?ISO-2022-JP?B?GyRCNGNLPBsoQg==?=) FLAM-DOODLE/1.12.6
 (=?ISO-2022-JP?B?GyRCM3cbKEI=?= 10R4.0/5.0) Emacs/20.4
 (sparc-sun-solaris2.6) MULE/4.0 (HANANOEN)

In article <199908250824.KAA10555@beta.informatik.hu-berlin.de>,
  Sven Wischnowsky <wischnow@informatik.hu-berlin.de> writes:

> The syntax is, of course: `*::descr:action'.

I made new cvs completion functions using the syntax.
But it does not work well.

Z(2):akr@is27e1u11% ./Src/zsh -f
is27e1u11% bindkey -e; fpath=($PWD/Completion/*(/)); autoload -U compinit; compinit -D; compdef _tst tst
is27e1u11% compconf group_matches=yes
is27e1u11% compconf message_format='%d'
is27e1u11% compconf description_format='%d'
is27e1u11% cvs diff <TAB>
Completion/       Etc/              Src/              
Config/           Functions/        StartupFiles/     
Doc/              Misc/             Util/             
option
--allow-root=     -7                -T                -n
--help            -8                -U                -p
--help-commands   -9                -W                -q
--help-options    -B                -a                -r
--help-synonyms   -C                -b                -s
--version         -D                -c                -t
-0                -F                -d                -u
-1                -H                -e                -v
-2                -I                -f                -w
-3                -L                -h                -x
-4                -N                -i                -z
-5                -Q                -k                
-6                -R                -l                

cvs common options such as --version are completed addition to options
for cvs diff. This behavious is reproducible as follows:

is27e1u11% _tst () { _arguments '-x' '*::mesg:_tst2' }
is27e1u11% _tst2 () { compadd "$@" aaa }
is27e1u11% tst x x <TAB>
option
-x    
mesg
aaa   

is27e1u11% cvs -e vi  
unknown cvs command: -e
option
--allow-root=     -H                -f                -t
--help            -Q                -l                -v
--help-commands   -T                -n                -w
--help-options    -a                -q                -x
--help-synonyms   -b                -r                -z
--version         -d                -s                

_arguments does not cutoff words in this case. Hm.

Index: Completion/Base/_arguments
===================================================================
RCS file: /projects/zsh/zsh/Completion/Base/_arguments,v
retrieving revision 1.1.1.12
diff -u -F^( -r1.1.1.12 _arguments
--- _arguments	1999/08/26 10:07:28	1.1.1.12
+++ _arguments	1999/08/26 10:32:26
@@ -331,9 +331,12 @@
 
       if [[ -n "$uns" ]]; then
 	uns="${(j::)${(@k)oneshot[(I)${ws[1][1]}[$uns]]#[-+]}}"
-	unset "opts[${(@)^opts[(I)${ws[1][1]}[$uns]]}]" \
-	      "dopts[${(@)^dopts[(I)${ws[1][1]}[$uns]]}]" \
-	      "odopts[${(@)^odopts[(I)${ws[1][1]}[$uns]]}]"
+	tmp=(
+	  "opts[${(@)^opts[(I)${ws[1][1]}[$uns]]}]"
+	  "dopts[${(@)^dopts[(I)${ws[1][1]}[$uns]]}]"
+	  "odopts[${(@)^odopts[(I)${ws[1][1]}[$uns]]}]"
+	)
+	(( $#tmp )) && unset "$tmp[@]"
       fi
 
       # If we didn't find a matching option description and we were
@@ -442,9 +445,12 @@
 
     if [[ -n "$uns" ]]; then
       uns="${(j::)${(@k)oneshot[(I)${ws[1][1]}[$uns]]#[-+]}}"
-      unset "opts[${(@)^opts[(I)${pre[1]}[$uns]]}]" \
-	    "dopts[${(@)^dopts[(I)${pre[1]}[$uns]]}]" \
-	    "odopts[${(@)^odopts[(I)${pre[1]}[$uns]]}]"
+      tmp=(
+	"opts[${(@)^opts[(I)${pre[1]}[$uns]]}]"
+	"dopts[${(@)^dopts[(I)${pre[1]}[$uns]]}]"
+	"odopts[${(@)^odopts[(I)${pre[1]}[$uns]]}]"
+      )
+      (( $#tmp )) && unset "$tmp[@]"
     fi
 
     if [[ -n "$opt" ]]; then
@@ -458,7 +464,7 @@
 	if [[ -n "$sopts" && -n "$PREFIX" && "$PREFIX" = [-+]${~soptseq}[$sopts] ]]; then
 	  if [[ "$PREFIX" = [-+]${~soptseq1} ]]; then
 	    compadd "$expl[@]" -Q \
-                    -y "( ${(k)opts} ${(k)dopts} ${(k)odopts} )" - \
+                    -y "( ${(j: :)${(@M)${(@k)opts}:#[-+]?}} ${(j: :)${(@M)${(@k)dopts}:#[-+]?}} ${(j: :)${(@M)${(@k)odopts}:#[-+]?}} )" - \
                     "${PREFIX}${(@k)^opts[(I)${PREFIX[1]}?]#?}" \
 		    "${PREFIX}${(@k)^dopts[(I)${PREFIX[1]}?]#?}" \
 		    "${PREFIX}${(@k)^odopts[(I)${PREFIX[1]}?]#?}" && ret=0
diff -Naur Completion/Cvs/_cvs Completion/Cvs+/_cvs
--- Completion/Cvs/_cvs	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs	Thu Aug 26 19:27:22 1999
@@ -0,0 +1,14 @@
+#compdef cvs
+
+# "+Qqrwtnlvb:T:e:d:Hfz:s:xa"
+_arguments -s \
+  -{a,f,H,l,n,Q,q,r,t,v,w,x} \
+  '--version' '--help' '--help-commands' '--help-synonyms' '--help-options' \
+  '--allow-root=-:rootdir:_files -/' \
+  '-b+:bindir:_cvs_bindir' \
+  '-T+:temporary directory:_cvs_tempdir' \
+  '-d+:cvsroot:_cvs_root' \
+  '-e+:editor:_cvs_editor' \
+  '-s+:user variable:_cvs_user_variable' \
+  '-z:gzip level:_cvs_gzip_level' \
+  '*::cvs command:_cvs_command'
diff -Naur Completion/Cvs/_cvs_D Completion/Cvs+/_cvs_D
--- Completion/Cvs/_cvs_D	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_D	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" today yesterday week\ ago month\ ago
diff -Naur Completion/Cvs/_cvs_add Completion/Cvs+/_cvs_add
--- Completion/Cvs/_cvs_add	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_add	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,7 @@
+#autoload
+
+# "+k:m:"
+_arguments -s \
+  '-k+:keyword substitution:_cvs_k' \
+  '-m+:message:_cvs_m' \
+  '*:file:_cvs_files_unmaintained' \
diff -Naur Completion/Cvs/_cvs_admin Completion/Cvs+/_cvs_admin
--- Completion/Cvs/_cvs_admin	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_admin	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,21 @@
+#autoload
+
+# "+ib::c:a:A:e:l::u::LUn:N:m:o:s:t::IqxV:k:"
+_arguments -s \
+  -{i,L,U,I,q,x} \
+  '-b-:default branch:(1.1.1)' \
+  '-c+:comment leader (not used):' \
+  '-a+:login names (not work with CVS):' \
+  '-A+:access list to append (not work with CVS):' \
+  '-e+:access list to erase (not work with CVS):' \
+  '-l-:revision to lock:' \
+  '-u-:revision to unlock:' \
+  '-n+:symbolic-name[\:revision]:' \
+  '-N+:symbolic-name[\:revision]:' \
+  '-m+:revision\:msg:' \
+  '-o+:range to delete:' \
+  '-s+:state[\:revision]:' \
+  '-t-:descriptive text:_cvs_admin_t' \
+  '-V+:version (obsolete):' \
+  '-k+:keyword substitution:_cvs_k' \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_admin_t Completion/Cvs+/_cvs_admin_t
--- Completion/Cvs/_cvs_admin_t	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_admin_t	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,7 @@
+#autoload
+
+if compset -P -; then
+  _message 'descriptive text'
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_annotate Completion/Cvs+/_cvs_annotate
--- Completion/Cvs/_cvs_annotate	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_annotate	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,8 @@
+#autoload
+
+# "+lr:D:fR"
+_arguments -s \
+  -{l,f,R} \
+  '-r+:tag:_cvs_revisions' \
+  '-D+:date:_cvs_D' \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_bindir Completion/Cvs+/_cvs_bindir
--- Completion/Cvs/_cvs_bindir	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_bindir	Thu Aug 26 19:20:37 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" /usr/local/bin || _files "$@" -/
diff -Naur Completion/Cvs/_cvs_checkout Completion/Cvs+/_cvs_checkout
--- Completion/Cvs/_cvs_checkout	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_checkout	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,11 @@
+#autoload
+
+# "+ANnk:d:flRpQqcsr:D:j:P"
+_arguments -s \
+  -{A,N,n,f,l,R,q,c,s,P} \
+  '-k+:keyword substitution:_cvs_k' \
+  '-d+:directory:_files -/' \
+  '-r+:tag:_cvs_revisions' \
+  '-D+:date:_cvs_D' \
+  '-j+:tag:_cvs_revisions' \
+  '*:module:_cvs_modules'
diff -Naur Completion/Cvs/_cvs_command Completion/Cvs+/_cvs_command
--- Completion/Cvs/_cvs_command	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_command	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,42 @@
+#autoload
+
+typeset -A commands
+commands=(add "ad new"            admin "adm rcs"         annotate ann
+	  checkout "co get"       commit "ci com"         diff "di dif"
+	  edit ""                 editors ""              export "exp ex"
+	  history "hi his"        import "im imp"         init ""
+	  log "lo rlog"           login "logon lgn"       logout ""
+	  rdiff patch             release "re rel"        remove "rm delete"
+	  status "st stat"        rtag "rt rfreeze"       tag "ta freeze"
+	  unedit ""               update "up upd"         watch ""
+	  watchers "")
+
+if (( CURRENT == 1 )); then
+  compadd ${(k)commands} || compadd ${(kv)=commands}
+else
+  case "$words[1]" in
+    add|ad|new) _cvs_add;;
+    admin|adm|rcs) _cvs_admin;;
+    annotate|ann) _cvs_annotate;;
+    checkout|co|get) _cvs_checkout;;
+    commit|ci|com) _cvs_commit;;
+    diff|di|dif) _cvs_diff;;
+    edit) _cvs_edit;;
+    editors) _cvs_editors;;
+    export|exp|ex) _cvs_export;;
+    history|hi|his) _cvs_history;;
+    import|im|imp) _cvs_import;;
+    init) _cvs_init;;
+    login|logon|lgn|logout) _cvs_login;;
+    rdiff|patch|pa) _cvs_rdiff;;
+    release|re|rel) _cvs_release;;
+    remove|rm|delete) _cvs_remove;;
+    status|st|stat) _cvs_status;;
+    tag|ta|freeze) _cvs_tag;;
+    unedit) _cvs_unedit;;
+    update|up|upd) _cvs_update;;
+    watch) _cvs_watch;;
+    watchers) _cvs_watchers;;
+    *) _message "unknown cvs command: $words[1]";;
+  esac
+fi
diff -Naur Completion/Cvs/_cvs_commit Completion/Cvs+/_cvs_commit
--- Completion/Cvs/_cvs_commit	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_commit	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,9 @@
+#autoload
+
+# "+nlRm:fF:r:"
+_arguments -s \
+  -{n,l,R,f} \
+  '-m+:message:_cvs_m' \
+  '-F+:log message file:_files' \
+  '-r+:tag:_cvs_revisions' \
+  '*:file:_cvs_files_modified'
diff -Naur Completion/Cvs/_cvs_diff Completion/Cvs+/_cvs_diff
--- Completion/Cvs/_cvs_diff	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_diff	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,15 @@
+#autoload
+
+# "+abcdefhilnpstuw0123456789BHNRC:D:F:I:L:U:V:W:k:r:"
+_arguments -s \
+  -{a,b,c,d,e,f,h,i,l,n,p,s,t,u,w,0,1,2,3,4,5,6,7,8,9,B,H,N,R} \
+  '-C:lines:' \
+  '-D+:date:_cvs_D' \
+  '-F+:regex:' \
+  '-I+:regex:' \
+  '*-L+:label:' \
+  '-U:lines:' \
+  '-W:columns:' \
+  '-k+:keyword substitution:_cvs_k' \
+  '-r+:tag:_cvs_revisions' \
+  '*:file:_cvs_diff_arg'
diff -Naur Completion/Cvs/_cvs_diff_arg Completion/Cvs+/_cvs_diff_arg
--- Completion/Cvs/_cvs_diff_arg	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_diff_arg	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+_cvs_files_modified || _cvs_files
diff -Naur Completion/Cvs/_cvs_directories Completion/Cvs+/_cvs_directories
--- Completion/Cvs/_cvs_directories	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_directories	Thu Aug 26 19:30:18 1999
@@ -0,0 +1,8 @@
+#autoload
+
+if [[ -d ${pref}CVS ]]; then
+  _cvs_setup_direntries
+  (( $#entries )) && compgen "$@" -g "${(j:|:)${(@)entries:q}}"
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_edit Completion/Cvs+/_cvs_edit
--- Completion/Cvs/_cvs_edit	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_edit	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,7 @@
+#autoload
+
+# "+lRa:"
+_arguments -s \
+  -{l,R} \
+  '-a+:action:(edit unedit commit all none)'
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_editor Completion/Cvs+/_cvs_editor
--- Completion/Cvs/_cvs_editor	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_editor	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" vi
diff -Naur Completion/Cvs/_cvs_editors Completion/Cvs+/_cvs_editors
--- Completion/Cvs/_cvs_editors	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_editors	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+lR"
+_arguments -s \
+  -{l,R} \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_export Completion/Cvs+/_cvs_export
--- Completion/Cvs/_cvs_export	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_export	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,10 @@
+#autoload
+
+# "+Nnk:d:flRQqr:D:"
+_arguments -s \
+  -{N,n,f,l,R,Q,q} \
+  '-k+:keyword substitution:_cvs_k' \
+  '-d+:directory:_files -/' \
+  '-r+:tag:_cvs_revisions' \
+  '-D+:date:_cvs_D' \
+  '*:module:_cvs_modules'
diff -Naur Completion/Cvs/_cvs_extract_directory_entries Completion/Cvs+/_cvs_extract_directory_entries
--- Completion/Cvs/_cvs_extract_directory_entries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_extract_directory_entries	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+entries=($entries ${${${(M)rawentries:#D/*}#D/}%%/*})
diff -Naur Completion/Cvs/_cvs_extract_file_entries Completion/Cvs+/_cvs_extract_file_entries
--- Completion/Cvs/_cvs_extract_file_entries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_extract_file_entries	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+entries=($entries ${${${(M)rawentries:#/*}#/}%%/*})
diff -Naur Completion/Cvs/_cvs_extract_modifiedfile_entries Completion/Cvs+/_cvs_extract_modifiedfile_entries
--- Completion/Cvs/_cvs_extract_modifiedfile_entries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_extract_modifiedfile_entries	Thu Aug 26 19:30:32 1999
@@ -0,0 +1,13 @@
+#autoload
+
+if [[ -n "$compconfig[_cvs_disable_stat]" ]] ||
+  ! { zmodload -e stat || zmodload stat }; then
+  _cvs_extract_file_entries
+  return
+fi
+
+local ents pats
+ents=(${${${${(M)rawentries:#/*}#/}/\\/[^\\/]#\\///}%/[^/]#/[^/]#})
+pats=(${${${(f)"$(LANG=C builtin stat -gn +mtime -F '%a %b %e %T %Y' ${pref}*(D))"}##*/}/ //})
+eval 'ents=(${ents:#('${(j:|:)${(@)pats:q}}')})'
+entries=($entries ${ents%%/*})
diff -Naur Completion/Cvs/_cvs_files Completion/Cvs+/_cvs_files
--- Completion/Cvs/_cvs_files	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_files	Thu Aug 26 19:30:43 1999
@@ -0,0 +1,10 @@
+#autoload
+
+local qpref pref entries
+_cvs_setup_prefix
+if [[ -d ${pref}CVS ]]; then
+  _cvs_setup_allentries
+  (( $#entries )) && compgen "$@" -g "${(j:|:)${(@)entries:q}}"
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_files_modified Completion/Cvs+/_cvs_files_modified
--- Completion/Cvs/_cvs_files_modified	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_files_modified	Thu Aug 26 19:29:54 1999
@@ -0,0 +1,10 @@
+#autoload
+
+local qpref pref entries
+_cvs_setup_prefix
+if [[ -d ${pref}CVS ]]; then
+  _cvs_setup_modentries
+  (( $#entries )) && compgen "$@" -g "${(j:|:)${(@)entries:q}}"
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_files_removed Completion/Cvs+/_cvs_files_removed
--- Completion/Cvs/_cvs_files_removed	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_files_removed	Thu Aug 26 19:30:53 1999
@@ -0,0 +1,15 @@
+#autoload
+
+local qpref pref entries
+_cvs_setup_prefix
+if [[ -d ${pref}CVS ]]; then
+  _cvs_setup_allentries
+  setopt localoptions unset
+  local omit
+  omit=(${pref}*(D:t))
+  eval 'entries=(${entries:#('${(j:|:)${(@)omit:q}}')})'
+  compadd "$@" -P "$qpref" - ${entries:q} ||
+  _cvs_directories "$@"
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_files_unmaintained Completion/Cvs+/_cvs_files_unmaintained
--- Completion/Cvs/_cvs_files_unmaintained	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_files_unmaintained	Thu Aug 26 19:31:16 1999
@@ -0,0 +1,26 @@
+#autoload
+
+if (( ! $+_cvs_ignore_default )); then
+  _cvs_ignore_default=(
+    RCS SCCS CVS CVS.adm RCSLOG 'cvslog.*' tags TAGS .make.state .nse_depinfo
+    '*\~' '\#*' '.\#*' ',*' '_$*' '*$' '*.old' '*.bak' '*.BAK' '*.orig' '*.rej'
+    '.del-*' '*.a' '*.olb' '*.o' '*.obj' '*.so' '*.exe' '*.Z' '*.elc' '*.ln'
+    core
+  )
+fi
+
+local qpref pref entries
+_cvs_setup_prefix
+if [[ -d ${pref}CVS ]]; then
+  _cvs_setup_allentries
+  setopt localoptions unset
+  local omit
+  omit=($_cvs_ignore_default ${entries:q} ${=cvsignore})
+  [[ -r ~/.cvsignore ]] && omit=($omit $(<~/.cvsignore))
+  [[ -r ${pref}.cvsignore ]] && omit=($omit $(<${pref}.cvsignore))
+  compgen "$@" -g '*~(*/|)('${(j:|:)omit}')(D)' ||
+  compgen "$@" -g '*~(*/|)('${(j:|:)${(@)entries:q}}')(D)' ||
+  _cvs_directories "$@"
+else
+  _files "$@"
+fi
diff -Naur Completion/Cvs/_cvs_gzip_level Completion/Cvs+/_cvs_gzip_level
--- Completion/Cvs/_cvs_gzip_level	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_gzip_level	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" 9
diff -Naur Completion/Cvs/_cvs_history Completion/Cvs+/_cvs_history
--- Completion/Cvs/_cvs_history	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_history	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,18 @@
+#autoload
+
+# "+Tacelow?D:b:f:m:n:p:r:t:u:x:X:z:"
+_arguments -s \
+  -{T,a,c,e,l,o,w,\?} \
+  '-D+:date:_cvs_D' \
+  '-b+:string:' \
+  '-f+:arg:' \
+  '-m+:module:_cvs_modules' \
+  '-n+:arg:' \
+  '*-p+:repository:' \
+  '-r+:rev:' \
+  '-t+:tag:' \
+  '-u+:user name:' \
+  '-x+:type:_cvs_history_x' \
+  '-X+:arg:' \
+  '-z+:arg:' \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_history_x Completion/Cvs+/_cvs_history_x
--- Completion/Cvs/_cvs_history_x	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_history_x	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,5 @@
+#autoload
+
+compset -P '*'
+# It should describe meaning.
+compadd F O E T C G U W A M R
diff -Naur Completion/Cvs/_cvs_import Completion/Cvs+/_cvs_import
--- Completion/Cvs/_cvs_import	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_import	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,13 @@
+#autoload
+
+# "+Qqdb:m:I:k:W:"
+_arguments -s \
+  -{Q,q,d} \
+  '-b+:branch:' \
+  '-m+:message:_cvs_m' \
+  '*-I+:name:_files' \
+  '-k+:keyword substitution:_cvs_k' \
+  '*-W+:spec:' \
+  ':repository:_cvs_modules' \
+  ':vendor tag:' \
+  ':release tag:'
diff -Naur Completion/Cvs/_cvs_init Completion/Cvs+/_cvs_init
--- Completion/Cvs/_cvs_init	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_init	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+false
diff -Naur Completion/Cvs/_cvs_k Completion/Cvs+/_cvs_k
--- Completion/Cvs/_cvs_k	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_k	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" kv kvl k o b v
diff -Naur Completion/Cvs/_cvs_login Completion/Cvs+/_cvs_login
--- Completion/Cvs/_cvs_login	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_login	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+false
diff -Naur Completion/Cvs/_cvs_m Completion/Cvs+/_cvs_m
--- Completion/Cvs/_cvs_m	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_m	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+_message "log message"
diff -Naur Completion/Cvs/_cvs_modules Completion/Cvs+/_cvs_modules
--- Completion/Cvs/_cvs_modules	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_modules	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,12 @@
+#autoload
+
+local root=$CVSROOT
+[[ -f CVS/Root ]] && root=$(<CVS/Root)
+
+if [[ $root = :* || ! -d $root ]]; then
+  _message "module name"
+else
+  compadd - \
+    $root/^CVSROOT(:t) \
+    ${${(M)${(f)"$(<$root/CVSROOT/modules)"}:#[^#]*}%%[ 	]*}
+fi
diff -Naur Completion/Cvs/_cvs_rdiff Completion/Cvs+/_cvs_rdiff
--- Completion/Cvs/_cvs_rdiff	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_rdiff	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,10 @@
+#autoload
+
+# "+V:k:cuftsQqlRD:r:"
+_arguments -s \
+  -{c,u,f,t,s,Q,q,l,R} \
+  '-V+:version:' \
+  '-k+:keyword substitution:_cvs_k' \
+  '*-D+:date:_cvs_D' \
+  '*-r+:tag:_cvs_revisions' \
+  '*:module:_cvs_modules'
diff -Naur Completion/Cvs/_cvs_release Completion/Cvs+/_cvs_release
--- Completion/Cvs/_cvs_release	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_release	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+Qdq"
+_arguments -s \
+  -{Q,d,q} \
+  '*:directory:_files -/'
diff -Naur Completion/Cvs/_cvs_remove Completion/Cvs+/_cvs_remove
--- Completion/Cvs/_cvs_remove	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_remove	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+flR"
+_arguments -s \
+  -{f,l,R} \
+  '*:file:_cvs_files_removed'
diff -Naur Completion/Cvs/_cvs_revisions Completion/Cvs+/_cvs_revisions
--- Completion/Cvs/_cvs_revisions	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_revisions	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd - ${${${(M)${(f)"$(cvs -q status -vl .)"}:#	*}##[ 	]##}%%[ 	]*}
diff -Naur Completion/Cvs/_cvs_root Completion/Cvs+/_cvs_root
--- Completion/Cvs/_cvs_root	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_root	Thu Aug 26 19:20:37 1999
@@ -0,0 +1,11 @@
+#autoload
+
+if (( ! $+_cvs_roots )); then
+  if [[ -f ~/.cvspass ]]; then
+    _cvs_roots=(${${(f)"$(<~/.cvspass)"}%% *})
+  else
+    _cvs_roots=()
+  fi
+fi
+
+compadd "$@" $_cvs_roots || _files "$@" -/
diff -Naur Completion/Cvs/_cvs_setup_allentries Completion/Cvs+/_cvs_setup_allentries
--- Completion/Cvs/_cvs_setup_allentries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_setup_allentries	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,9 @@
+#autoload
+
+entries=()
+if [[ -f ${pref}CVS/Entries ]]; then
+  local rawentries
+  rawentries=(${(f)"$(<${pref}CVS/Entries)"})
+  _cvs_extract_file_entries
+  _cvs_extract_directory_entries
+fi
diff -Naur Completion/Cvs/_cvs_setup_direntries Completion/Cvs+/_cvs_setup_direntries
--- Completion/Cvs/_cvs_setup_direntries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_setup_direntries	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,8 @@
+#autoload
+
+entries=()
+if [[ -f ${pref}CVS/Entries ]]; then
+  local rawentries
+  rawentries=(${(f)"$(<${pref}CVS/Entries)"})
+  _cvs_extract_directory_entries
+fi
diff -Naur Completion/Cvs/_cvs_setup_modentries Completion/Cvs+/_cvs_setup_modentries
--- Completion/Cvs/_cvs_setup_modentries	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_setup_modentries	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,9 @@
+#autoload
+
+entries=()
+if [[ -f ${pref}CVS/Entries ]]; then
+  local rawentries
+  rawentries=(${(f)"$(<${pref}CVS/Entries)"})
+  _cvs_extract_modifiedfile_entries
+  _cvs_extract_directory_entries
+fi
diff -Naur Completion/Cvs/_cvs_setup_prefix Completion/Cvs+/_cvs_setup_prefix
--- Completion/Cvs/_cvs_setup_prefix	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_setup_prefix	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,9 @@
+#autoload
+
+if [[ -prefix */ ]]; then
+  qpref="${PREFIX%/*}/"
+  pref=$~qpref
+else
+  qpref=
+  pref=./
+fi
diff -Naur Completion/Cvs/_cvs_status Completion/Cvs+/_cvs_status
--- Completion/Cvs/_cvs_status	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_status	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+vlR"
+_arguments -s \
+  -{v,l,R} \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_tag Completion/Cvs+/_cvs_tag
--- Completion/Cvs/_cvs_tag	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_tag	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,8 @@
+#autoload
+
+# "+FQqlRcdr:D:bf"
+_arguments -s \
+  -{F,Q,q,l,R,c,d,b,f} \
+  '-r+:tag:_cvs_revisions' \
+  '-D+:date:_cvs_D' \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_tempdir Completion/Cvs+/_cvs_tempdir
--- Completion/Cvs/_cvs_tempdir	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_tempdir	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,3 @@
+#autoload
+
+compadd "$@" $TMPPREFIX:h $TMPDIR /tmp
diff -Naur Completion/Cvs/_cvs_unedit Completion/Cvs+/_cvs_unedit
--- Completion/Cvs/_cvs_unedit	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_unedit	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+lR"
+_arguments -s \
+  -{l,R} \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_update Completion/Cvs+/_cvs_update
--- Completion/Cvs/_cvs_update	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_update	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,12 @@
+#autoload
+
+# "+ApPflRQqduk:r:D:j:I:W:"
+_arguments -s \
+  -{A,p,P,f,l,R,Q,q,d,u} \
+  '-k+:keyword substitution:_cvs_k' \
+  '-r+:tag:_cvs_revisions' \
+  '-D+:date:_cvs_D' \
+  '-j+:tag:_cvs_revisions' \
+  '*-I+:name:_files' \
+  '*-W+:spec:' \
+  '*:file:_cvs_files'
diff -Naur Completion/Cvs/_cvs_user_variable Completion/Cvs+/_cvs_user_variable
--- Completion/Cvs/_cvs_user_variable	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_user_variable	Thu Aug 26 19:20:37 1999
@@ -0,0 +1,7 @@
+#autoload
+
+if compset -P '*='; then
+  _default
+else
+  _message "variable=value"
+fi
diff -Naur Completion/Cvs/_cvs_watch Completion/Cvs+/_cvs_watch
--- Completion/Cvs/_cvs_watch	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_watch	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,21 @@
+#autoload
+
+if (( CURRENT == 2 )); then
+  compadd on off add remove
+else
+  case "$words[2]" in
+    on|off) # "+lR"
+      _arguments -s \
+	-{l,R} \
+	':watch command:' \
+	':*:file:_cvs_files'
+      ;;
+    add|remove) # "+lRa:"
+      _arguments -s \
+	-{l,R} \
+	'*-a+:action:(edit unedit commit all none)' \
+	':watch command:' \
+	':*:file:_cvs_files'
+      ;;
+  esac
+fi
diff -Naur Completion/Cvs/_cvs_watchers Completion/Cvs+/_cvs_watchers
--- Completion/Cvs/_cvs_watchers	Thu Jan  1 09:00:00 1970
+++ Completion/Cvs+/_cvs_watchers	Thu Aug 26 19:20:38 1999
@@ -0,0 +1,6 @@
+#autoload
+
+# "+lR"
+_arguments -s \
+  -{l,R} \
+  ':*:file:_cvs_files'
-- 
Tanaka Akira

