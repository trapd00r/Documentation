### vim:ft=zsh:foldmethod=marker

if ! zis_317 "atleast"; then
    zprintf 2 'This zsh is too old for this wrapper, skipping setup.\n'
    return 0
fi
zstyle ':acceptline:empty' call_default false
zstyle ':acceptline:*' nocompwarn   true

function turn_dots_into_cd () {
    local buf="$1"
    buf='cd '
    for (( i = 1; i <= ${#${cmdline[1]}}; i++ )); do
        buf="${buf}../"
    done
    buf=${buf%/}
    REPLY="$buf"
}

function zle_cd_back() {
    local REPLY
    if (( ${#cmdline} == 1 )) && [[ ${cmdline[1]} == .# ]] ; then
        turn_dots_into_cd "${BUFFER}"
        BUFFER="$REPLY"
    fi
}

function zle_dir_stack() {
    if (( ${#cmdline} == 1 )); then
        case ${cmdline[1]} in
        ((+|-)(|<->))
            BUFFER="cd $BUFFER"
            [[ ${cmdline[1]} == + ]] && BUFFER="${BUFFER}0"
            ;;
        ((#b)-([lcvp]))
            BUFFER="dirs -${match[1]}"
            ;;
        esac
    fi
}

zle -N zle_cd_back
zle -N zle_dir_stack

zstyle ':acceptline:preprocess' actions zle_cd_back zle_dir_stack
