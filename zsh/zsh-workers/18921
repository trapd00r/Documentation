From zsh-workers-return-18921-mason-zsh=primenet.com.au@sunsite.dk Fri Aug 01 15:34:26 2003
Return-Path: <zsh-workers-return-18921-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 14051 invoked from network); 1 Aug 2003 15:34:25 -0000
Received: from sunsite.dk (130.225.247.90)
  by ns1.primenet.com.au with SMTP; 1 Aug 2003 15:34:25 -0000
Received: (qmail 28757 invoked by alias); 1 Aug 2003 15:34:21 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 18921
Received: (qmail 28744 invoked from network); 1 Aug 2003 15:34:20 -0000
Received: from localhost (HELO sunsite.dk) (127.0.0.1)
  by localhost with SMTP; 1 Aug 2003 15:34:20 -0000
X-MessageWall-Score: 0 (sunsite.dk)
Received: from [193.109.254.211] by sunsite.dk (MessageWall 1.0.8) with SMTP; 1 Aug 2003 15:34:20 -0000
X-VirusChecked: Checked
X-Env-Sender: okiddle@yahoo.co.uk
X-Msg-Ref: server-23.tower-36.messagelabs.com!1059738048!107425
X-StarScan-Version: 5.0.7; banners=-,-,-
Received: (qmail 22451 invoked from network); 1 Aug 2003 11:40:48 -0000
Received: from iris.logica.co.uk (158.234.9.163)
  by server-23.tower-36.messagelabs.com with SMTP; 1 Aug 2003 11:40:48 -0000
Received: from gmcs3.local ([158.234.142.61])
	by iris.logica.co.uk (8.12.3/8.12.3/Debian -4) with ESMTP id h71BYK3l018455
	for <zsh-workers@sunsite.dk>; Fri, 1 Aug 2003 12:34:20 +0100
Received: from gmcs3.local (localhost [127.0.0.1])
	by gmcs3.local (8.11.6/8.11.6/SuSE Linux 0.5) with ESMTP id h71BZlg03515
	for <zsh-workers@sunsite.dk>; Fri, 1 Aug 2003 13:35:51 +0200
X-VirusChecked: Checked
X-StarScan-Version: 5.0.7; banners=.,-,-
From: Oliver Kiddle <okiddle@yahoo.co.uk>
To: Zsh workers <zsh-workers@sunsite.dk>
Subject: PATCH: new chmod completion
Date: Fri, 01 Aug 2003 13:35:47 +0200
Message-ID: <3513.1059737747@gmcs3.local>

Like _chown, it will only complete files for which the command would be
meaningful so after chmod -x, you'll only get executable files.

It doesn't handle some of the more obscure types of priviliges as they
would be very hard to do in shell code (and a lot more easily handled by
extending the C code for the f glob qualifier).

I've only tested this on Linux and Solaris so if any other UNIX has
different options or doesn't like it running chmod --version to check
for GNU then let me know.

Oliver

#compdef chmod

local curcontext="$curcontext" state line ret=1
local -a args privs

args=( '*:file:->files' )
(( $+words[(r)--reference*] )) || args+=( '1:mode:->mode' )

if _pick_variant gnu=Free\ Soft unix --version; then
  args+=(
    '(-v --verbose -c --changes)'{-c,--changes}'[report changes made]'
    '(-v --verbose -c --changes)'{-v,--verbose}'[output a diagnostic for every file processed]'
    '(-f --silent --quiet)'{-f,--silent,--quiet}'[suppress most error messages]'
    '--reference=[copy permissions of specified file]:file:_files'
    '(-R --recursive)'{-R,--recursive}'[change files and directories recursively]'
    '(- : *)--help[display help information]'
    '(- : *)--version[display version information]'
  )
  privs=(
    'X[execute only if executable to another]'
    "u[owner's current permissions]"
    "g[group's current permissions]"
    "o[other's current permissions]"
  )
else
  # based on $OSTYPE = solaris2.8
  args+=(
    '-f[suppress most error messages]'
    '-R[change files and directories recursively]'
  )
  privs=( 'l[mandatory locking]' )
fi

_arguments -C -s "$args[@]" && ret=0

case "$state" in
  mode)
    compset -P \*,
    compset -S ,\*
    if [[ -prefix [0-7] ]]; then
      _message -e number 'numeric mode'
    elif compset -P '[a-z]#[+-=]'; then
      _values -S '' privilege \
	'r[read]' 'w[write]' 'x[execute]' \
	's[set uid/gid]' 't[sticky]' \
	"$privs[@]" && ret=0
    else
      suf=( -S '' )
      compset -P '*'
      _alternative -O suf \
	'who:who:((u\:user g\:group a\:all o\:others))' \
	'operators:operator:(+ - =)'
    fi
  ;;
  files)
    if [[ -n $opt_args[--reference] ]]; then
      if zstyle -t ":completion:${curcontext}:" disable-stat; then
	_files && ret=0
      else
	zmodload -i zsh/stat 2>/dev/null
	typeset -i8 ref=$(stat +mode $opt_args[--reference])
	_wanted files expl file _files -g "*(-.^f${ref#??})" && ret=0
      fi
    elif [[ $words[2] = [0-7]## ]]; then
      _wanted files expl file _files -g "*(-.^f$words[2])" && ret=0
    else
      local spec who op priv
      local -a specs
      for spec in ${(s:,:)words[2]}; do
	if [[ ${spec#*[+-=]} != [rwxst]* ]]; then
	  _files && ret=0
	  return ret
	fi

	specs+=( ${${(M)spec##[+-=]*}:+a}$spec )
      done
      _wanted files expl file _files -g "*(-.^f:${(j.,.)specs}:)" && ret=0
    fi
  ;;
esac

return ret

