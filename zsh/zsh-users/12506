From zsh-users-return-12506-mason-zsh=primenet.com.au@sunsite.dk Wed Jan 30 12:22:38 2008
Return-Path: <zsh-users-return-12506-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 21984 invoked from network); 30 Jan 2008 12:22:36 -0000
X-Spam-Checker-Version: SpamAssassin 3.2.4 (2008-01-01) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.4 required=5.0 tests=AWL,BAYES_00 autolearn=ham
	version=3.2.4
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 30 Jan 2008 12:22:36 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 71496 invoked from network); 30 Jan 2008 12:22:23 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 30 Jan 2008 12:22:23 -0000
Received: (qmail 20247 invoked by alias); 30 Jan 2008 12:22:15 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 12506
Received: (qmail 20234 invoked from network); 30 Jan 2008 12:22:15 -0000
Received: from bifrost.dotsrc.org (130.225.254.106)
  by sunsite.dk with SMTP; 30 Jan 2008 12:22:15 -0000
Received: from cluster-d.mailcontrol.com (cluster-d.mailcontrol.com [217.69.20.190])
	by bifrost.dotsrc.org (Postfix) with ESMTP id CD94A8029406
	for <zsh-users@sunsite.dk>; Wed, 30 Jan 2008 13:22:10 +0100 (CET)
Received: from cameurexb01.EUROPE.ROOT.PRI ([62.189.241.200])
	by rly49d.srv.mailcontrol.com (MailControl) with ESMTP id m0UCM787010800
	for <zsh-users@sunsite.dk>; Wed, 30 Jan 2008 12:22:08 GMT
Received: from news01.csr.com ([10.103.143.38]) by cameurexb01.EUROPE.ROOT.PRI with Microsoft SMTPSVC(6.0.3790.1830);
	 Wed, 30 Jan 2008 12:22:06 +0000
Received: from news01.csr.com (localhost.localdomain [127.0.0.1])
	by news01.csr.com (8.14.1/8.13.4) with ESMTP id m0UCM5LQ005692
	for <zsh-users@sunsite.dk>; Wed, 30 Jan 2008 12:22:05 GMT
Received: from csr.com (pws@localhost)
	by news01.csr.com (8.14.1/8.14.1/Submit) with ESMTP id m0UCM5aq005689
	for <zsh-users@sunsite.dk>; Wed, 30 Jan 2008 12:22:05 GMT
Message-Id: <200801301222.m0UCM5aq005689@news01.csr.com>
X-Authentication-Warning: news01.csr.com: pws owned process doing -bs
To: zsh-users@sunsite.dk
Subject: Re: removing spaces from a file name
In-reply-to: <237967ef0801300356q6fe00738ha11723966ce400fe@mail.gmail.com>
References: <Xns9A3574A71769Dzzappergmailcom@80.91.229.13> <237967ef0801300341u1af07b2l1a4aece5614e09dd@mail.gmail.com> <237967ef0801300356q6fe00738ha11723966ce400fe@mail.gmail.com>
Comments: In-reply-to "Mikael Magnusson" <mikachu@gmail.com>
   message dated "Wed, 30 Jan 2008 12:56:05 +0100."
Date: Wed, 30 Jan 2008 12:22:05 +0000
From: Peter Stephenson <pws@csr.com>
X-OriginalArrivalTime: 30 Jan 2008 12:22:06.0162 (UTC) FILETIME=[BA1AA320:01C8633A]
X-Scanned-By: MailControl A-06-00-00 (www.mailcontrol.com) on 10.68.0.159
X-Virus-Scanned: ClamAV 0.91.2/5605/Wed Jan 30 12:04:40 2008 on bifrost
X-Virus-Status: Clean

"Mikael Magnusson" wrote:
> > You could define a function in your .zshrc (or make a script in your
> > ~/bin or something) that does it for you
> > function renamespaces() { for a ($@) {mv $a ${a// } } }
> 
> If you're the kind of person who likes keybinds, I guess this works
> too, if you have copy-earlier-word bound somewhere too.

That's quite useful... here's another more general widget that operates on
the current word, prompting for source and replacement strings (as with
Mikael's, if you use it with mv it's more work).  This essentially just
replaces the history substitution in the original posting with something
a bit more intuitive.


# replace-in-current-word
# Prompt for source and replacement text for current argument.
# Currently only handles strings, not patterns.
# Operates on the unquoted argument, but takes care to requote
# it (with backslashes).  Hence, for example, a command line argument
#   foo\*\ bar
# when given " " to replace with "_" produces
#   foo\*_bar
# (with no backslash before the "_").
#
# Based on replace-string: can use previous source and replacement
# strings using the mechanism documented for that (via numeric argument
# or style).

emulate -L zsh
setopt extendedglob

autoload -U read-from-minibuffer modify-current-argument

local p1="Replace: " p2="   with: "

if (( ${+NUMERIC} )); then
  (( $NUMERIC > 0 )) && previous=1
else
  zstyle -t ":zle:$WIDGET" edit-previous && previous=1
fi

read-from-minibuffer $p1 ${previous:+$_replace_word_src} || return 1
_replace_word_src=$REPLY

read-from-minibuffer "$p1$_replace_word_src$p2" \
  ${previous:+$_replace_word_rep} || return 1
_replace_word_rep=$REPLY

modify-current-argument '${(q)${(Q)ARG//$_replace_word_src/$_replace_word_rep}}'



-- 
Peter Stephenson <pws@csr.com>                  Software Engineer
CSR PLC, Churchill House, Cambridge Business Park, Cowley Road
Cambridge, CB4 0WZ, UK                          Tel: +44 (0)1223 692070

