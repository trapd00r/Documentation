From zsh-users-return-14219-mason-zsh=primenet.com.au@sunsite.dk Sun Jul 05 19:50:57 2009
Return-Path: <zsh-users-return-14219-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 18536 invoked from network); 5 Jul 2009 19:50:54 -0000
X-Spam-Checker-Version: SpamAssassin 3.2.5 (2008-06-10) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.6 required=5.0 tests=AWL,BAYES_00 autolearn=ham
	version=3.2.5
Received: from new-brage.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.254.104)
  by ns1.primenet.com.au with SMTP; 5 Jul 2009 19:50:54 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 64741 invoked from network); 5 Jul 2009 19:50:43 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 5 Jul 2009 19:50:43 -0000
Received: (qmail 694 invoked by alias); 5 Jul 2009 19:50:23 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 14219
Received: (qmail 684 invoked from network); 5 Jul 2009 19:50:22 -0000
Received: from bifrost.dotsrc.org (130.225.254.106)
  by sunsite.dk with SMTP; 5 Jul 2009 19:50:22 -0000
Received: from mtaout02-winn.ispmail.ntl.com (mtaout02-winn.ispmail.ntl.com [81.103.221.48])
	by bifrost.dotsrc.org (Postfix) with ESMTP id 5939B8027106
	for <zsh-users@sunsite.dk>; Sun,  5 Jul 2009 21:50:18 +0200 (CEST)
Received: from aamtaout03-winn.ispmail.ntl.com ([81.103.221.35])
          by mtaout02-winn.ispmail.ntl.com
          (InterMail vM.7.08.04.00 201-2186-134-20080326) with ESMTP
          id <20090705195018.LMBF6611.mtaout02-winn.ispmail.ntl.com@aamtaout03-winn.ispmail.ntl.com>
          for <zsh-users@sunsite.dk>; Sun, 5 Jul 2009 20:50:18 +0100
Received: from pws-pc ([81.107.42.185]) by aamtaout03-winn.ispmail.ntl.com
          (InterMail vG.2.02.00.01 201-2161-120-102-20060912) with ESMTP
          id <20090705195017.IEYI2093.aamtaout03-winn.ispmail.ntl.com@pws-pc>
          for <zsh-users@sunsite.dk>; Sun, 5 Jul 2009 20:50:17 +0100
Date: Sun, 5 Jul 2009 20:50:10 +0100
From: Peter Stephenson <p.w.stephenson@ntlworld.com>
To: zsh-users@sunsite.dk
Subject: Re: Mimicking tcsh line-editing behavior
Message-ID: <20090705205010.0596d662@pws-pc>
In-Reply-To: <198303.88532.qm@web23507.mail.ird.yahoo.com>
References: <198303.88532.qm@web23507.mail.ird.yahoo.com>
X-Mailer: Claws Mail 3.7.1 (GTK+ 2.16.2; x86_64-redhat-linux-gnu)
Mime-Version: 1.0
Content-Type: text/plain; charset=US-ASCII
Content-Transfer-Encoding: 7bit
X-Cloudmark-Analysis: v=1.0 c=1 a=_eppES1DROUA:10 a=LtGpxU-LAAAA:8 a=NLZqzBF-AAAA:8 a=beJHLC1dWTAxlhkVbMEA:9 a=kqcwgBqoiPE3Zsf-ezEA:7 a=Zvc_qafJNCsyGndWuUdJfhpa-AUA:4 a=eL4mtSqiQiEA:10 a=_dQi-Dcv4p4A:10
X-Virus-Scanned: ClamAV 0.94.2/9538/Fri Jul  3 16:27:11 2009 on bifrost
X-Virus-Status: Clean

On Thu, 2 Jul 2009 19:46:19 +0000 (GMT)
Rhyme Tan <rhymetan@rocketmail.com> wrote:
> My default shell is tcsh. In tcsh I can use alt(meta)-backspace, alt-F or alt-B to delete, or move forward or backward through a path.
> 
> For example I type:
> 
> ls /An/Example/of/a/Path/Name/
> 
> To delete Path/Name/, I simply hold down the alt key and press
> backspace twice (alt-backspace x2). This will leave me with the path
> &quot;/An/Example/of/a/&quot;. I cannot do this in zsh because pressing
> alt-backspace will delete the whole path (that is,
> &quot;/An/Example/of/a/Path/Name/&quot;). Is there some magic config
> option I can pass to zsh to duplicate the behavior of alt-backspace, -F,
> or -B) in tcsh?

Sounds like the simple solution

WORDCHARS=${WORDCHARS//\/}

is OK for you.

For anyone trying to do more, look at the zshcontrib manual for
select-word-style which allows you to do all sorts of things like this
very easily, with different configurations for different bindings, and
even different places on the command line and words matching different
patterns.

The following is what I have, but the possibilities are endless.

    # This activates the system and makes whitespace-separated
    # words the default.
    autoload -U select-word-style
    select-word-style whitespace

    # This sets up a function that doesn't treat / as a word character.
    # It only binds it for backward-kill-word, but all the normal
    # word functions can be rebound similarly.
    zstyle ':zle:backward-kill-word-slash*' word-style normal
    zstyle ':zle:backward-kill-word-slash*' word-chars ${WORDCHARS//\/}
    zlewidget '^x/' backward-kill-word-slash backward-kill-word-match

    # This (completely separate from the above) extends word contexts
    # so that "whitespace" is space betwen words, anything with
    # a slash in is "filename", anything with a comma or equals is
    # "list", the end of the line is "end", anything else is "other".
    zstyle ':zle:*' word-context "[[:space:]]" whitespace "*/*" \
      filename "*[,=]*" list "" end "*" other
    # Transposing words swaps shell words if you're between words.
    zstyle ':zle:transpose-words:whitespace' word-style shell
    # Transposing words only swaps alphanumeric characters when
    # in a string with a /
    zstyle ':zle:transpose-words:filename' word-style normal
    zstyle ':zle:transpose-words:filename' word-chars ''
    # Any of the kill functions use a reduced notion of word characters
    # (i.e. stop killing on a larger set of characters) when in
    # an argument that looks like a list.
    zstyle ':zle:*kill-*:list' word-style normal
    zstyle ':zle:*kill-*:list' word-chars '-_.~'

zlewidget is just a simple shortcut:

   zlewidget() {
     zle -N $2 $3
     bindkey $1 $2
  }

Comments about the system are welcome, it's supposed to be general
enough to prevent people reinventing the wheel, but apparently wasn't...

-- 
Peter Stephenson <p.w.stephenson@ntlworld.com>
Web page now at http://homepage.ntlworld.com/p.w.stephenson/

