From zsh-workers-return-18890-mason-zsh=primenet.com.au@sunsite.dk Wed Jul 23 11:04:50 2003
Return-Path: <zsh-workers-return-18890-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 10403 invoked from network); 23 Jul 2003 11:04:49 -0000
Received: from sunsite.dk (130.225.247.90)
  by ns1.primenet.com.au with SMTP; 23 Jul 2003 11:04:49 -0000
Received: (qmail 14908 invoked by alias); 23 Jul 2003 11:04:38 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 18890
Received: (qmail 14893 invoked from network); 23 Jul 2003 11:04:37 -0000
Received: from localhost (HELO sunsite.dk) (127.0.0.1)
  by localhost with SMTP; 23 Jul 2003 11:04:37 -0000
X-MessageWall-Score: 0 (sunsite.dk)
Received: from [193.109.254.211] by sunsite.dk (MessageWall 1.0.8) with SMTP; 23 Jul 2003 11:4:37 -0000
X-VirusChecked: Checked
X-Env-Sender: okiddle@yahoo.co.uk
X-Msg-Ref: server-5.tower-36.messagelabs.com!1058958275!30940
X-StarScan-Version: 5.0.7; banners=.,-,-
Received: (qmail 6915 invoked from network); 23 Jul 2003 11:04:35 -0000
Received: from iris.logica.co.uk (158.234.9.163)
  by server-5.tower-36.messagelabs.com with SMTP; 23 Jul 2003 11:04:35 -0000
Received: from gmcs3.local ([158.234.142.61])
	by iris.logica.co.uk (8.12.3/8.12.3/Debian -4) with ESMTP id h6NB4Z3l021307
	for <zsh-workers@sunsite.dk>; Wed, 23 Jul 2003 12:04:35 +0100
Received: from gmcs3.local (localhost [127.0.0.1])
	by gmcs3.local (8.11.6/8.11.6/SuSE Linux 0.5) with ESMTP id h6NB5tI08373
	for <zsh-workers@sunsite.dk>; Wed, 23 Jul 2003 13:05:55 +0200
X-VirusChecked: Checked
From: Oliver Kiddle <okiddle@yahoo.co.uk>
To: Zsh workers <zsh-workers@sunsite.dk>
Subject: PATCH: new sabcmd completion
Date: Wed, 23 Jul 2003 13:05:55 +0200
Message-ID: <8371.1058958355@gmcs3.local>

New completion function for sabcmd (an xslt processor (like xsltproc)
from the sablotron package).

#compdef sabcmd

local curcontext="$curcontext" state mode line expl ret=1
typeset -A opt_args
local mopts='(-c -x -s -? -v --chain-xsl --batch-xml --batch-xsl --help --version)'

_arguments -C \
  '(-b --base)'{-b,--base=}'[specify base URI]:URI' \
  '(-)--debug-options[show help on debug options]' \
  '(- *)'{-\?,--help}'[show help information]' \
  '(-L --log-file)'{-L,--log-file=}'[specify log file, turn on logging]:log file:_files' \
  '(-m --measure)'{-m,--measure}'[measure the processing time]' \
  '(- *)'{-v,--version}'[display version information]' \
  '--debugger[run the xslt debugger]' \
  $mopts{-c,--chain-xsl}'[single input file, multiple chained stylesheets]' \
  $mopts{-x,--batch-xml}'[single input file, multiple stylesheets]' \
  $mopts{-s,--batch-xsl}'[multiple input files, single stylesheet]' \
  '*: :->arguments' && ret=0

if [[ -n "$state" ]]; then
  mode="${opt_args[(i)-c|--chain-xsl]:+chain}${opt_args[(i)-x|--batch-xml]:+batch-x}${opt_args[(i)-s|--batch-xsl]:+batch-s}"
  [[ $mode = (chain|batch-[xs]) ]] || mode=default
  _tags assignments files
  while _tags; do
    if _requested assignments; then
      if [[ -prefix *= ]]; then
        _message -e value value
      else
        _message -e parameter 'parameter name'
      fi
    fi
    if _requested files; then
      case $mode:${#words[1,CURRENT-1]:#(-*|*=*)}; in
        default:1|chain:<3->|batch-x:2|batch-s:1)
	  _description files expl stylesheet
	  _files "$expl[@]" -g \*.xsl && ret=0
	;;
        default:2|chain:1|batch-x:1|batch-s:2)
	  _description files expl 'input file'
	  _files "$expl[@]" && ret=0
	;;
        default:3|chain:2|batch-x:<3->|batch-s:<3->)
	  _description files expl 'output file'
	  _files "$expl[@]" && ret=0
	;;
      esac
    fi
    (( ret )) || break
  done
fi

return ret

________________________________________________________________________
This email has been scanned for all viruses by the MessageLabs Email
Security System. For more information on a proactive email security
service working around the clock, around the globe, visit
http://www.messagelabs.com
________________________________________________________________________

