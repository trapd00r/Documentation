From zsh-workers-return-8296-mason-zsh=primenet.com.au@sunsite.auc.dk Sat Oct 16 05:12:27 1999
Return-Path: <zsh-workers-return-8296-mason-zsh=primenet.com.au@sunsite.auc.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 5499 invoked from network); 16 Oct 1999 05:12:25 -0000
Received: from sunsite.auc.dk (130.225.51.30)
  by ns1.primenet.com.au with SMTP; 16 Oct 1999 05:12:25 -0000
Received: (qmail 9317 invoked by alias); 16 Oct 1999 05:12:20 -0000
Mailing-List: contact zsh-workers-help@sunsite.auc.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.auc.dk
X-Seq: 8296
Received: (qmail 9309 invoked from network); 16 Oct 1999 05:12:19 -0000
To: zsh-workers@sunsite.auc.dk
Subject: PATCH: _whois
MIME-Version: 1.0 (generated by AKEMI 1.13.2 - =?ISO-2022-JP?B?Ig==?=
 =?ISO-2022-JP?B?GyRCQTA0Y0s8GyhCIg==?=)
Content-Type: text/plain; charset=US-ASCII
From: Tanaka Akira <akr@jaist.ac.jp>
Date: 16 Oct 1999 14:12:17 +0900
Message-ID: <rsq1zav51ou.fsf@crane.jaist.ac.jp>
Lines: 222
User-Agent: Chao-gnus/6.12.5 AKEMI/1.13.2 (=?ISO-2022-JP?B?GyRCQTAbKEI=?=
 =?ISO-2022-JP?B?GyRCNGNLPBsoQg==?=) FLAM-DOODLE/1.12.6
 (=?ISO-2022-JP?B?GyRCM3cbKEI=?= 10R4.0/5.0) Emacs/20.4
 (sparc-sun-solaris2.6) MULE/4.0 (HANANOEN)

I wrote a completion function for whois.

Since whois has many variant (than telnet), it may not support some
variant.  I saw FreeBSD, BSD/OS, SunOS, Slackware and Debian for
reference.  At least it doesn't support ripe-whois enough yet.

--- /dev/null	Sat Oct 16 12:11:10 1999
+++ Completion/User/_whois	Sat Oct 16 14:05:29 1999
@@ -0,0 +1,211 @@
+#compdef whois
+
+_whois () {
+  setopt localoptions extendedglob
+  _whois_setup
+  $_whois_comp
+}
+
+builtin functions _whois_setup >&- ||
+_whois_setup () {
+  (( $+_whois_defaultserver )) ||
+    _whois_defaultserver='whois.internic.net'
+
+  (( $+_whois_servers )) || {
+    typeset -gUa _whois_servers
+    _whois_servers=(
+      $_whois_defaultserver
+      domain-registry.nl
+      is.nic.pw
+      whois.apnic.net:p
+      whois.arin.net:a
+      whois.aunic.net
+      whois.berkeley.edu
+      whois.cdnnet.ca
+      whois.dns.pt
+      whois.funet.fi
+      whois.gb.com
+      whois.gb.net
+      whois.internic.net
+      whois.jpl.nasa.gov
+      whois.nic-se.se
+      whois.nic.ad.jp
+      whois.nic.af
+      whois.nic.as
+      whois.nic.br
+      whois.nic.bt
+      whois.nic.ch
+      whois.nic.cx
+      whois.nic.fr
+      whois.nic.gov:g
+      whois.nic.hm
+      whois.nic.it
+      whois.nic.li
+      whois.nic.lk
+      whois.nic.mil:d
+      whois.nic.mx
+      whois.nic.nu
+      whois.nic.or.kr
+      whois.nic.sh
+      whois.nic.tj
+      whois.nic.tm
+      whois.nic.uk
+      whois.ripe.net:r
+      whois.ripn.net:R
+      whois.sics.se
+      whois.stanford.edu
+      whois.uk.com
+      www.nic.at
+      $(functions -m '_whois:*' |
+	awk '/^undefined _whois:/ {print substr($2,8,length($2)-7)}
+	     /^_whois:/ {print substr($1,8,length($1)-7)}')
+    )
+  }
+
+  (( $+_whois_arguments )) || {
+    local help="$(whois </dev/null 2>&1)"
+    local tmp opt opts
+
+    if [[ $help = *"user[@<whois.server>]"* ]]; then
+      _whois_comp=_whois_fwhois
+    elif [[ $help = *(name\ ...|OBJECT...)* ]]; then
+      _whois_comp=_whois_multi
+    else
+      _whois_comp=_whois_single
+    fi
+
+    _whois_arguments=()
+
+    if [[ $help = *"-p PORT"* ]]; then
+      _whois_arguments=("$_whois_arguments[@]"
+        '-p[port]:port:_whois_ports'
+      )
+    fi
+
+    tmp="${(j::)${(@)${(@M)_whois_servers:#*:?}##*:}}" 
+    if [[ $help = (#b)*\[-([$tmp]##)\]* ]]; then
+      tmp=(${(s::)match[1]})
+    else
+      tmp=()
+    fi
+
+    if [[ $help = *"-h host"* ]]; then
+      tmp=($tmp h)
+    fi
+
+    for opt in $tmp; do
+      opts=(-${^tmp:#$opt})
+      if (( $#opts )); then opts="($opts)"; else opts=; fi
+      _whois_arguments=("$_whois_arguments[@]"
+	"${opts}-${opt}[${${${(@M)_whois_servers:#*:$opt}%:?}:-specify host}]${(M)${(M)opt:#h}/h/:host:_whois_hosts}"
+      )
+    done
+  }
+}
+
+_whois_single () {
+  local state line expl
+  typeset -A opt_args
+  local tmp host
+
+  _arguments \
+    "$_whois_arguments[@]" \
+    ':identifier:->identifier'
+
+  case "$state" in
+  identifier)
+    if [[ -z "$QIPREFIX" ]]; then
+      compadd -QS '' \'
+      return
+    fi
+    compset -q
+    host="${opt_args[-h]:-$_whois_defaultserver}"
+    for tmp in $_whois_servers; do
+      if [[ $tmp = *:? && $+opt_args[-${tmp##*:}] -ne 0 ]]; then
+	host="${tmp%:?}"
+        break
+      fi
+    done
+    if builtin functions "_whois:$host" >&-; then
+      "_whois:$host" "$expl[@]"
+    else
+      _message "identifier"
+    fi
+    ;;
+  esac
+}
+
+_whois_multi () {
+  local state line expl
+  typeset -A opt_args
+  local tmp host
+
+  _arguments \
+    "$_whois_arguments[@]" \
+    '*::identifier:->identifier'
+
+  case "$state" in
+  identifier)
+    host="${opt_args[-h]:-$_whois_defaultserver}"
+    for tmp in $_whois_servers; do
+      if [[ $tmp = *:? && $+opt_args[-${tmp##*:}] -ne 0 ]]; then
+	host="${tmp%:?}"
+        break
+      fi
+    done
+    if builtin functions "_whois:$host" >&-; then
+      "_whois:$host" "$expl[@]"
+    else
+      _message "identifier"
+    fi
+    ;;
+  esac
+}
+
+_whois_fwhois () {
+  if compset -P '*@'; then
+    _whois_hosts "$@"
+  else
+    if [[ -z "$QIPREFIX" ]]; then
+      compadd -QS '' \'
+      return
+    fi
+    compset -q
+    host="$_whois_defaultserver"
+    if builtin functions "_whois:$host" >&-; then
+      "_whois:$host" "$@"
+    else
+      _message "identifier"
+    fi
+  fi
+}
+
+_whois_hosts () {
+  compadd "$@" \
+    -M 'm:{a-zA-Z}={A-Za-z} r:|.=* r:|=*' \
+    - ${_whois_servers%:?} || _hosts "$@"
+}
+
+_whois_ports () {
+  compadd "$@" - whois || _ports "$@"
+}
+
+builtin functions _whois:whois.internic.net >&- ||
+_whois:whois.internic.net () {
+  if (( CURRENT == 1 )); then
+    compadd HELP DOMAIN HOST
+  else
+    _message 'string'
+  fi
+}
+
+builtin functions _whois:whois.nic.ad.jp >&- ||
+_whois:whois.nic.ad.jp () {
+  if (( CURRENT == 1 )); then
+    compadd HELP DOM NET HOST PERSON CONN COM
+  else
+    _message 'string'
+  fi
+}
+
+_whois "$@"
-- 
Tanaka Akira

