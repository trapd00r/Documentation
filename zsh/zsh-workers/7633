From zsh-workers-return-7633-mason-zsh=primenet.com.au@sunsite.auc.dk Thu Sep 02 18:23:19 1999
Return-Path: <zsh-workers-return-7633-mason-zsh=primenet.com.au@sunsite.auc.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 1640 invoked from network); 2 Sep 1999 18:23:17 -0000
Received: from sunsite.auc.dk (130.225.51.30)
  by ns1.primenet.com.au with SMTP; 2 Sep 1999 18:23:17 -0000
Received: (qmail 324 invoked by alias); 2 Sep 1999 18:23:04 -0000
Mailing-List: contact zsh-workers-help@sunsite.auc.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.auc.dk
X-Seq: 7633
Received: (qmail 316 invoked from network); 2 Sep 1999 18:23:03 -0000
To: zsh-workers@sunsite.auc.dk
Subject: PATCH: _lynx and _urls.
MIME-Version: 1.0 (generated by AKEMI 1.13.2 - =?ISO-2022-JP?B?Ig==?=
 =?ISO-2022-JP?B?GyRCQTA0Y0s8GyhCIg==?=)
Content-Type: text/plain; charset=US-ASCII
From: Tanaka Akira <akr@jaist.ac.jp>
Date: 03 Sep 1999 03:23:01 +0900
Message-ID: <rsqemghqika.fsf@crane.jaist.ac.jp>
Lines: 184
User-Agent: Chao-gnus/6.12.5 AKEMI/1.13.2 (=?ISO-2022-JP?B?GyRCQTAbKEI=?=
 =?ISO-2022-JP?B?GyRCNGNLPBsoQg==?=) FLAM-DOODLE/1.12.6
 (=?ISO-2022-JP?B?GyRCM3cbKEI=?= 10R4.0/5.0) Emacs/20.4
 (sparc-sun-solaris2.6) MULE/4.0 (HANANOEN)

This is completion function for lynx.

--- /dev/null	Fri Sep  3 02:54:25 1999
+++ Completion/User/_lynx	Fri Sep  3 03:13:53 1999
@@ -0,0 +1,109 @@
+#compdef lynx
+
+_lynx () {
+  _arguments \
+    '-accept_all_cookies' \
+    '-anonymous' \
+    '-assume_charset=:MIMENAME:' \
+    '-assume_local_charset=:MIMENAME:' \
+    '-assume_unrec_charset=:MIMENAME:' \
+    '-auth=:ID\:PW:' \
+    '-base' \
+    '-blink' \
+    '-book' \
+    '-buried_news' \
+    '-cache=:NUMBER:' \
+    '-case' \
+    '-cfg=:FILENAME:_files' \
+    '-child' \
+    '-color' \
+    '-cookies' \
+    '-core' \
+    '-crawl' \
+    '-display=:DISPLAY:_x_display' \
+    '-dump' \
+    '-editor=:EDITOR:_files' \
+    '-emacskeys' \
+    '-enable_scrollback' \
+    '-error_file=:FILENAME:_files' \
+    '-fileversions' \
+    '-force_html' \
+    '-force_secure' \
+    '-from' \
+    '-ftp' \
+    '-get_data' \
+    '-head' \
+    '-help' \
+    '-historical' \
+    '-homepage=:URL: _urls -f' \
+    '-image_links' \
+    '-ismap' \
+    '-index=:URL: _urls -f' \
+    '-link=:NUMBER:' \
+    '-localhost' \
+    '-locexec' \
+    '-mime_header' \
+    '-minimal' \
+    '-newschunksize=:NUMBER:' \
+    '-newsmaxchunk=:NUMBER:' \
+    '-nobrowse' \
+    '-nocc' \
+    '-nocolor' \
+    '-noexec' \
+    '-nofilereferer' \
+    '-nolist' \
+    '-nolog' \
+    '-nonrestarting_sigwinch' \
+    '-nopause' \
+    '-noprint' \
+    '-noredir' \
+    '-noreferer' \
+    '-nosocks' \
+    '-nostatus' \
+    '-number_links' \
+    '-partial' \
+    '-pauth=:ID\:PW:' \
+    '-popup' \
+    '-post_data' \
+    '-preparsed' \
+    '-print' \
+    '-pseudo_inlines' \
+    '-raw' \
+    '-realm' \
+    '-reload' \
+    '-restrictions=:options:_lynx_restrictions' \
+    '-resubmit_posts' \
+    '-rlogin' \
+    '-selective' \
+    '-show_cursor' \
+    '-soft_dquotes' \
+    '-source' \
+    '-startfile_ok' \
+    '-tagsoup' \
+    '-telnet' \
+    '-term=:TERM:' \
+    '-tlog' \
+    '-trace' \
+    '-traversal' \
+    '-underscore' \
+    '-useragent=:NAME:' \
+    '-use_mouse' \
+    '-validate' \
+    '-verbose' \
+    '-version' \
+    '-vikeys' \
+    '-width=:NUMBER:' \
+    ':url: _urls -f'
+}
+
+_lynx_restrictions () {
+  compset -P '*,'
+  compadd -qS, \
+    all bookmark bookmark_exec change_exec_perms default dired_support \
+    disk_save dotfiles download editor exec exec_frozen externals file_url \
+    goto inside_ftp inside_news inside_rlogin inside_telnet jump mail \
+    multibook news_post options_save outside_ftp outside_news outside_rlogin \
+    outside_telnet print shell suspend telnet_port useragent
+}
+
+_lynx "$@"
--- /dev/null	Fri Sep  3 02:54:25 1999
+++ Completion/User/_urls	Fri Sep  3 03:09:54 1999
@@ -0,0 +1,65 @@
+#autoload
+
+# Usage: _urls [-f]
+# Options:
+#  -f : complete files.
+
+# To complete URLs, you must make a URL database locally such as:
+#
+# % cd ~/.zsh/urls
+# % find . -ls
+# ... drwxr-xr-x ... 512 Sep  3 02:46 .
+# ... drwxr-xr-x ... 512 Sep  3 02:48 ./http
+# ... drwxr-xr-x ... 512 Sep  3 02:52 ./http/www.zsh.org
+# ... drwxr-xr-x ... 512 Sep  3 03:01 ./http/www.zsh.org/mla
+# ... drwxr-xr-x ... 512 Sep  3 03:01 ./http/www.zsh.org/mla/workers
+# ... drwxr-xr-x ... 512 Sep  3 03:01 ./http/www.zsh.org/mla/workers/1999
+# ... -rw-r--r-- ...   0 Sep  3 03:01 ./http/www.zsh.org/mla/workers/1999/index.html
+# ... drwxr-xr-x ... 512 Sep  3 02:48 ./http/sunsite.auc.dk
+# ... drwxr-xr-x ... 512 Sep  3 02:48 ./http/sunsite.auc.dk/zsh
+# ... drwxr-xr-x ... 512 Sep  3 02:47 ./bookmark
+# ... drwxr-xr-x ... 512 Sep  3 02:48 ./bookmark/zsh
+# ... -rw-r--r-- ...  27 Sep  3 02:47 ./bookmark/zsh/home
+# ... -rw-r--r-- ...  20 Sep  3 02:48 ./bookmark/zsh/meta
+
+local ipre scheme dirs files
+
+if [[ "$1" = -f ]]; then
+  shift
+  _files "$@" && return
+fi
+
+if [[ -z "$compconfig[_urls_dir]" ]]; then
+  compconfig[_urls_dir]=${ZDOTDIR:-$HOME}/.zsh/urls
+fi
+
+ipre="$IPREFIX"
+
+if [[ -prefix [-+.a-z0-9]#: ]]; then
+  scheme="${PREFIX%%:*}"
+  compset -P "[-+.a-z0-9]#:"
+else
+  compadd -S '' http:// ftp:// bookmark:
+  return
+fi
+
+case "$scheme" in
+  http) compset -P // || { compadd "$@" -S '' //; return };;
+  ftp) compset -P // || { compadd "$@" -S '' //; return };;
+esac
+
+if [[ "$scheme" = bookmark &&
+      -f "$compconfig[_urls_dir]/$scheme/$PREFIX$SUFFIX" &&
+      -s "$compconfig[_urls_dir]/$scheme/$PREFIX$SUFFIX" ]]; then
+  compadd "$@" -QU -- "$ipre$(<"$compconfig[_urls_dir]/$scheme/$PREFIX$SUFFIX")"
+else
+  dirs=($compconfig[_urls_dir]/$scheme/$PREFIX*$SUFFIX(/:t))
+  files=($compconfig[_urls_dir]/$scheme/$PREFIX*$SUFFIX(.:t))
+  compset -P '*/'
+  compadd "$@" -Q -S '/' - $dirs
+  if [[ "$scheme" = bookmark ]]; then
+    compadd "$@" -QS '' - $files
+  else
+    compadd "$@" -Q - $files
+  fi
+fi
-- 
Tanaka Akira

