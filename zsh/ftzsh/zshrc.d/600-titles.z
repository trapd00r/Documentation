### vim:ft=zsh:foldmethod=marker

# This should be pretty safe for most strings that are thrown at us.
if zis_314 "atleast"; then
    function screen_printf () {
        local format esc
        esc=$'\e'
        format="$1"
        shift
        printf '%s' "${esc}k"
        printf "${format}" "$@"
        printf '%s' "${esc}"\\
    }
else
    function screen_printf () {
    }
fi

### put the current program (or 'zsh') into screen's hardstatus
if ! zis_437 "atleast" ; then
    function precmd () {
        if [[ "$TERM" == screen* ]]; then
            if [[ -n ${SSH_CLIENT} ]] ; then
                screen_printf '%s: %s' "${(%):-"%m"}" "zsh"
            else
                screen_printf '%s' "zsh"
            fi
        fi
    }
else
    # Okay then. If we're this new, we got add-zsh-hook and vcs_info.
    # So, let's play.
    function precmd_status () {
        local zsh
        if [[ "$TERM" == screen* ]]; then
            if [[ -n ${vcs_info_msg_1_} ]] ; then
                zsh=${vcs_info_msg_1_}
            else
                zsh='zsh'
            fi
            if [[ -n ${SSH_CLIENT} ]] ; then
                screen_printf '%s: %s' "${(%):-"%m"}" "${zsh}"
            else
                screen_printf '%s' "${zsh}"
            fi
        fi
    }
    add-zsh-hook precmd precmd_status
fi

function preexec_status () {
    setopt localoptions extendedglob
    local CMD
    local -a words

    words=( ${(z)1} )
    while (( ${#words} > 0 )) ; do
        case ${words[1]} in
            *=*)    shift words ;;
            sudo)   shift words ;;
            -*)     shift words ;;
            *) break ;;
        esac
    done

    case ${words[1]} in
        git|cvs|svn|bzr|hg|p4)
            CMD="${words[1]} ${words[2]}" ;;
        *)  CMD="${words[1]}" ;;
    esac

    if [[ -n ${SSH_CLIENT} ]] ; then
        screen_printf '%s: %s' "${(%):-"%m"}" "${CMD}"
    else
        screen_printf '%s' "${CMD}"
    fi
}

function preexec () {
    if [[ "$TERM" == screen* ]]; then
        preexec_status "$@"
    fi

}
