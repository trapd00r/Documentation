### vim:ft=zsh:foldmethod=marker
###
### Mass recoding Audio tags from ISO8859-15 to UTF8.
### Heavily based in atag-reset.
###
### Needs iconv.
###
### @@webhtml_contact@@
### Last-Modified: @@webhtml_last_modified@@
###
### URI: <@@webhtml_uri@@>
###

emulate -L zsh
setopt extendedglob
setopt nullglob
setopt noksharrays

local file dryrun rc needtorecode i
local -i ret__
local -a opts

if [[ -z ${commands[ataglist]} ]] || \
   [[ ! -x ${commands[ataglist]} ]] ; then

   printf 'ataglist not found. Giving up.\n'
   return 1
fi

if [[ $1 == '-d' ]] ; then
    shift
    opts=(-f -d)
else
    opts=(-f)
fi

printf ' *** Processing %s files.\n' ${#argv}

for file in "$@" ; do
    atag ${file} -e
    needtorecode=0
    for i in _al _ar _tt _gr _comp; do
        printf '%s' ${(P)i} | iconv --from-code=UTF8 &> /dev/null
        rc="$?"
        #if [[ ${(P)i} == *[^[:ascii:]]* ]] ; then
        if (( $rc == 0 )) && [[ ${(P)i} == *[^[:ascii:]]* ]] ; then
            needtorecode=1
            typeset -g $i="$(printf '%s' ${(P)i} | iconv --from-code=UTF8 --to-code=ISO8859-15)"
        fi
    done
    if (( ${needtorecode} == 0 )) ; then
        printf ' *** No need to recode "%s"\n' ${file}
        continue
    fi
    atag ${file} ${opts}    \
        al=${_al}           \
        ar=${_ar}           \
        tt=${_tt}           \
        tn=${_tn}           \
        y=${_yr}            \
        g=${_gr}            \
        comp=${_comp}
    (( ret__ += $? ))
done

return ${ret__}
