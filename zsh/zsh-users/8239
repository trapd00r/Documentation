From zsh-users-return-8239-mason-zsh=primenet.com.au@sunsite.dk Thu Nov 25 20:22:16 2004
Return-Path: <zsh-users-return-8239-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 22739 invoked from network); 25 Nov 2004 20:22:15 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 25 Nov 2004 20:22:15 -0000
Received: (qmail 4856 invoked from network); 25 Nov 2004 20:22:09 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 25 Nov 2004 20:22:09 -0000
Received: (qmail 7530 invoked by alias); 25 Nov 2004 20:22:02 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 8239
Received: (qmail 7520 invoked from network); 25 Nov 2004 20:22:01 -0000
Received: from unknown (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 25 Nov 2004 20:22:01 -0000
Received: (qmail 3578 invoked from network); 25 Nov 2004 20:21:02 -0000
Received: from moonbase.zanshin.com (64.84.47.139)
  by a.mx.sunsite.dk with SMTP; 25 Nov 2004 20:21:00 -0000
Received: from toltec.zanshin.com (toltec.zanshin.com [64.84.47.166])
	by moonbase.zanshin.com (8.13.1/8.13.1) with ESMTP id iAPKKwKe004385;
	Thu, 25 Nov 2004 12:20:59 -0800
Date: Thu, 25 Nov 2004 12:20:58 -0800 (PST)
From: Bart Schaefer <schaefer@brasslantern.com>
Reply-To: zsh-users@sunsite.dk
To: Ziggy <myrmidon@vfemail.net>
cc: zsh-users@sunsite.dk
Subject: Re: VMS Style auto command-complition
In-Reply-To: <1101407389.31116.9.camel@localhost>
Message-ID: <Pine.LNX.4.61.0411251113010.10283@toltec.zanshin.com>
References: <1101407389.31116.9.camel@localhost>
MIME-Version: 1.0
Content-Type: TEXT/PLAIN; charset=US-ASCII
X-Spam-Checker-Version: SpamAssassin 2.63 on a.mx.sunsite.dk
X-Spam-Level: 
X-Spam-Status: No, hits=0.0 required=6.0 tests=none autolearn=no version=2.63
X-Spam-Hits: 0.0

On Thu, 25 Nov 2004, Ziggy wrote:

> For example - if I want to purge all the previous versions of all files
> in the directory, instead of typing 'PURGE' I can type 'PU' because
> there is no any other command that starts with PU
[...]
> %DCL-W-ABVERB, ambiguous command verb - supply more characters
> \P\

In the ambiguous case shown above, is it actually prompting you to input 
more characters, or is that just an error message?

There's a whole lot of subtlety that may be going on here -- for example, 
what happens if the command is part of a complex construct like a "while" 
loop, or is executed from a script or batch file? -- some of which it may 
not be possible to emulate without help from the OS.

However, you could try this:

----------
function conditional-accept-line {
  emulate -L zsh
  if [[ -z "$PREBUFFER" ]]
  then
    local -a words commands
    words=(${(z)BUFFER})
    if [[ $words[1] != *=* && $words[1] != [({})] ]] &&
       ! whence -w $words[1] > /dev/null
    then
      commands=(${${(f)"$(noglob whence -m ${words[1]}* 2>/dev/null)"}:#})
      case $#commands in
      (0) zle -M "no such command: $words[1]"; return 1;;
      (1) BUFFER=${BUFFER/$words[1]/$commands};;
      (*) zle -M "ambiguous: $words[1]"; return 1;;
      esac
    fi
  fi
  zle .accept-line
}

zle -N accept-line conditional-accept-line
----------

Note, however, that this disables zsh's "setopt correct" feature, because 
correction takes place only after the line has been accepted.

