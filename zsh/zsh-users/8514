From zsh-users-return-8514-mason-zsh=primenet.com.au@sunsite.dk Thu Feb 17 05:41:53 2005
Return-Path: <zsh-users-return-8514-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 23099 invoked from network); 17 Feb 2005 05:41:50 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 17 Feb 2005 05:41:50 -0000
Received: (qmail 7462 invoked from network); 17 Feb 2005 05:41:41 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 17 Feb 2005 05:41:41 -0000
Received: (qmail 21100 invoked by alias); 17 Feb 2005 05:41:25 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 8514
Received: (qmail 21082 invoked from network); 17 Feb 2005 05:41:23 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 17 Feb 2005 05:41:23 -0000
Received: (qmail 6334 invoked from network); 17 Feb 2005 05:41:23 -0000
Received: from out005pub.verizon.net (HELO out005.verizon.net) (206.46.170.143)
  by a.mx.sunsite.dk with SMTP; 17 Feb 2005 05:41:19 -0000
Received: from candle.brasslantern.com ([4.11.10.129])
          by out005.verizon.net
          (InterMail vM.5.01.06.06 201-253-122-130-106-20030910) with ESMTP
          id <20050217054117.WCOI6130.out005.verizon.net@candle.brasslantern.com>;
          Wed, 16 Feb 2005 23:41:17 -0600
Received: from candle.brasslantern.com (IDENT:schaefer@localhost [127.0.0.1])
	by candle.brasslantern.com (8.12.11/8.12.11) with ESMTP id j1H5fGkW018120;
	Wed, 16 Feb 2005 21:41:16 -0800
Received: (from schaefer@localhost)
	by candle.brasslantern.com (8.12.11/8.12.11/Submit) id j1H5fGZq018119;
	Wed, 16 Feb 2005 21:41:16 -0800
From: Bart Schaefer <schaefer@brasslantern.com>
Message-Id: <1050217054116.ZM18118@candle.brasslantern.com>
Date: Thu, 17 Feb 2005 05:41:16 +0000
In-Reply-To: <20050217030355.GA2943@spiegl.de>
Comments: In reply to Andy Spiegl <zsh.Andy@spiegl.de>
        "parameter expansion or regex question" (Feb 17,  4:03am)
References: <20050217030355.GA2943@spiegl.de>
X-Mailer: Z-Mail (5.0.0 30July97)
To: Andy Spiegl <zsh.Andy@spiegl.de>, ZSH User List <zsh-users@sunsite.dk>
Subject: Re: parameter expansion or regex question
MIME-Version: 1.0
Content-Type: text/plain; charset=us-ascii
X-Authentication-Info: Submitted using SMTP AUTH at out005.verizon.net from [4.11.10.129] at Wed, 16 Feb 2005 23:41:17 -0600
X-Spam-Checker-Version: SpamAssassin 3.0.2 on a.mx.sunsite.dk
X-Spam-Level: 
X-Spam-Status: No, score=-2.6 required=6.0 tests=AWL,BAYES_00 autolearn=ham 
	version=3.0.2
X-Spam-Hits: -2.6

On Feb 17,  4:03am, Andy Spiegl wrote:
}
} In Perl I'd do it similar to this:
}  FILENAME =~ /(^|.)q[1-5](.|$)/;
} But I have no idea how/whether zsh can handle regexps.

The closest approximation to (the corrected form of) that perl is:

setopt extendedglob
CLEANFILENAME="${FILENAME/((#s)|.)q<1-5>(.|(#e))/}"

Another regex-like way would be:
 
setopt extendedglob
CLEANFILENAME="${FILENAME/(#b)(#s)(|*).##q<1-5>.#(*|)(#e)/$match[1]$match[2]}"

But you could also use (no extendedglob needed):

CLEANFILENAME="${(j:.:)${(@)${(@s:.:)FILENAME}:#q<1-5>}}"

The latter two solutions need adjustment if the file name may contain
multiple consecutive dots.

The last splits the file into an array at dots, discards all elements
that look like q<1-5>, then joins the remainder back together again.

The way you were trying to do it:

prefix="${${${(M)FILENAME#*q<1-5>.}:-${FILENAME%.q<1-5>*}}%q<1-5>.}"
suffix="${${${(M)FILENAME%.q<1-5>*}:-${FILENAME#*q<1-5>.}}#.q<1-5>}"
CLEANFILENAME="${prefix}${suffix#.}"

Spelled out:

prefix="${(M)FILENAME#*q<1-5>.}"
if [[ -n "$prefix" ]]; then prefix="${FILENAME%.q<1-5>*}"; fi
prefix="${prefix%q<1-5>.}"

suffix="${(M)FILENAME%.q<1-5>*}"
if [[ -n "$suffix" ]]; then suffix="${FILENAME#*q<1-5>.}"; fi
suffix="${suffix#.q<1-5>}"
suffix="${suffix#.}"

CLEANFILENAME="${prefix}${suffix}"

