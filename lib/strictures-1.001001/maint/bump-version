#!/usr/bin/env perl

use 5.010;
use strict;
use warnings FATAL => 'all';
use autodie;

chomp(my $LATEST = qx(grep '^[0-9]' Changes | head -1 | awk '{print \$1}'));

my @parts = split /\./, $LATEST;

my $OLD_DECIMAL = sprintf('%i.%03i%03i', @parts);

my %bump_part = (major => 0, minor => 1, bugfix => 2);

my $bump_this = 
  $bump_part{$ARGV[0]||'bugfix'}
    // die "no idea which part to bump - $ARGV[0] means nothing to me";

my @new_parts = @parts;

$new_parts[$bump_this]++;

my $NEW_DECIMAL = sprintf('%i.%03i%03i', @new_parts);

warn "Bumping $OLD_DECIMAL -> $NEW_DECIMAL\n";

my $PM_FILE = 'lib/Module/Metadata.pm';

my $file = do { local (@ARGV, $/) = ($PM_FILE); <> };

$file =~ s/(?<=\$VERSION = ')${\quotemeta $OLD_DECIMAL}/${NEW_DECIMAL}/;

open my $out, '>', $PM_FILE;

print $out $file;
