From zsh-workers-return-25702-mason-zsh=primenet.com.au@sunsite.dk Sun Sep 21 14:03:58 2008
Return-Path: <zsh-workers-return-25702-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 6923 invoked from network); 21 Sep 2008 14:03:40 -0000
X-Spam-Checker-Version: SpamAssassin 3.2.5 (2008-06-10) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.9 required=5.0 tests=AWL,BAYES_00 autolearn=ham
	version=3.2.5
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 21 Sep 2008 14:03:40 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 65567 invoked from network); 21 Sep 2008 14:03:31 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 21 Sep 2008 14:03:31 -0000
Received: (qmail 18320 invoked by alias); 21 Sep 2008 14:03:25 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 25702
Received: (qmail 18306 invoked from network); 21 Sep 2008 14:03:25 -0000
Received: from bifrost.dotsrc.org (130.225.254.106)
  by sunsite.dk with SMTP; 21 Sep 2008 14:03:25 -0000
Received: from uucp.gnuu.de (banki.eumelnet.de [83.246.114.63])
	by bifrost.dotsrc.org (Postfix) with ESMTP id 9A91D80308B9
	for <zsh-workers@sunsite.dk>; Sun, 21 Sep 2008 16:03:10 +0200 (CEST)
Received: by uucp.gnuu.de (Postfix, from userid 10)
	id D3DAE488035; Sun, 21 Sep 2008 16:02:59 +0200 (CEST)
X-DKIM: Sendmail DKIM Filter v2.5.2 uucp.gnuu.de D3DAE488035
DKIM-Signature: v=1; a=rsa-sha256; c=simple/simple; d=gnuu.de; s=banki;
	t=1222005779; i=@alea.gnuu.de; bh=wKGqwAdNqpD9veR9mUpfVJc0W/SjdFN3A
	lPvoK4fku8=; h=To:From:Subject:Date:Message-ID:Mime-Version:
	 Content-Type:Content-Transfer-Encoding:Sender; b=EH9Y4t2kRN2ucf1AE
	iF51Ki5QDI7FB6GNtjkwgxyzT9ng6aYT/mWanofsz5fPjrJmZHi1mWB1iXuv/oBmnzl
	EzNjUifTxy3+/O8SnkHdO5uysMw4VH731GhwwOsl40mjVU7uK+5luBCmDgcVAcmgBcc
	nIt82R6EMZGp1f4n0LaU=
Received: from news by alea.gnuu.de with local (Exim 4.63)
	(envelope-from <news@alea.gnuu.de>)
	id 1KhPT4-00083Q-N7
	for zsh-workers@sunsite.dk; Sun, 21 Sep 2008 15:59:18 +0200
To: zsh-workers@sunsite.dk
Path: not-for-mail
From: =?UTF-8?Q?J=C3=B6rg?= Sommer <joerg@alea.gnuu.de>
Newsgroups: local.mailinglist.zsh
Subject: [PATCH] Completion for chrt and ionice
Date: Sun, 21 Sep 2008 13:59:14 +0000 (UTC)
Lines: 122
Message-ID: <slrngdckpi.23d.joerg@alea.gnuu.de>
Mime-Version: 1.0
Content-Type: text/plain; charset=UTF-8
Content-Transfer-Encoding: 8bit
X-Trace: alea.gnuu.de 1222005554 30962 192.168.0.5 (21 Sep 2008 13:59:14 GMT)
X-Complaints-To: usenet@alea.gnuu.de
User-Agent: slrn/0.9.9 (Linux)
Sender: news <news@alea.gnuu.de>
X-Virus-Scanned: ClamAV 0.92.1/8297/Sun Sep 21 14:46:43 2008 on bifrost
X-Virus-Status: Clean

Hi,

commit 14b01890163625d1aa41610beb46e00155090308
Author: Jörg Sommer <joerg@alea.gnuu.de>
Date:   Sun Sep 21 15:51:36 2008 +0200

    Completion for the Linux command ionice
    
    The command ionice is used on Linux to get or set the IO scheduling
    attributes of a process.

diff --git a/Completion/Linux/Command/_ionice b/Completion/Linux/Command/_ionice
new file mode 100644
index 0000000..e3b64aa
--- /dev/null
+++ b/Completion/Linux/Command/_ionice
@@ -0,0 +1,23 @@
+#compdef ionice
+
+local context state line
+typeset -A opt_args
+
+_arguments \
+  '(* -)-h[display usage information]' \
+  '-p[interpret args as process ID]' \
+  '-c[scheduling class]:class:(( 1\:realtime 2\:best-effort 3\:idle ))' \
+  '-n[scheduling class priority]:class-pririty:((
+    0\:high\ priority
+    {1..6}\:
+    7\:low\ priority
+  ))' \
+  '*::command or pid:->cmd_or_pid' \
+  && return 0
+
+if (( $+opt_args[-p] ))
+then
+    _pids
+else
+    _normal
+fi

commit fc7231b2bd501b2a3dc753567c2ae8c4deddf9e7
Author: Jörg Sommer <joerg@alea.gnuu.de>
Date:   Sun Sep 21 15:50:10 2008 +0200

    Completion for the Linux command chrt
    
    The command chrt is used on Linux to get or set the real‐time attributes
    of a process.

diff --git a/Completion/Linux/Command/_chrt b/Completion/Linux/Command/_chrt
new file mode 100644
index 0000000..f82ec8b
--- /dev/null
+++ b/Completion/Linux/Command/_chrt
@@ -0,0 +1,61 @@
+#compdef chrt
+
+local context state line
+typeset -A opt_args
+
+_arguments \
+  '(* -)'{-h,--help}'[display usage information]' \
+  '(* -)'{-V,--version}'[output version information]' \
+  {-v,--verbose}'[display status information]' \
+  {-p,--pid}'[interpret args as process ID]' \
+  '(-b --batch -f --fifo -o --other -r --rr)'{-b,--batch}'[set scheduling policy to SCHED_BATCH]' \
+  '(-b --batch -f --fifo -o --other -r --rr)'{-f,--fifo}'[set scheduling policy to SCHED_FIFO]' \
+  '(-b --batch -f --fifo -o --other -r --rr)'{-o,--other}'[set policy scheduling policy to SCHED_OTHER]' \
+  '(-b --batch -f --fifo -o --other -r --rr)'{-r,--rr}'[set scheduling policy to SCHED_RR]' \
+  '(* -)'{-m,--max}'[show minimum and maximum valid priorities, then exit]' \
+  '*::command or priority:->cmd_or_prio' \
+  && return 0
+
+_chrt_priority()
+{
+    local ty
+    if (( $+opt_args[-b] || $+opt_args[--batch] ))
+    then
+        ty=BATCH
+    elif (( $+opt_args[-f] || $+opt_args[--fifo] ))
+    then
+        ty=FIFO
+    elif (( $+opt_args[-o] || $+opt_args[--other] ))
+    then
+        ty=OTHER
+    else
+        ty=RR
+    fi
+    local range
+    range=${${"$(_call_program priorities chrt --max)"#*SCHED_$ty*: }%$'\n'*}
+    if [[ $range = 0/0 ]]
+    then
+        compadd 0
+    else
+        _message -e priority "priority in the range $range"
+    fi
+}
+
+if (( $+opt_args[-p] || $+opt_args[--pid] ))
+then
+    if [[ $CURRENT -eq 1 ]]
+    then
+        _alternative \
+          'priority:priority:_chrt_priority' \
+          'processes:process IDs:_pids'
+    else
+        _pids
+    fi
+elif [[ $CURRENT -eq 1 ]]
+then
+    _chrt_priority
+else
+    shift words
+    (( CURRENT-- ))
+    _normal
+fi

Bye, Jörg.
-- 
Macht besitzen und nicht ausüben ist wahre Größe.
                                                   (Friedl Beutelrock)

