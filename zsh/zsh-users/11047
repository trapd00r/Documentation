From zsh-users-return-11047-mason-zsh=primenet.com.au@sunsite.dk Tue Nov 28 17:55:02 2006
Return-Path: <zsh-users-return-11047-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 26800 invoked from network); 28 Nov 2006 17:54:58 -0000
X-Spam-Checker-Version: SpamAssassin 3.1.7 (2006-10-05) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.4 required=5.0 tests=AWL,BAYES_00,
	FORGED_RCVD_HELO autolearn=ham version=3.1.7
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 28 Nov 2006 17:54:58 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 45647 invoked from network); 28 Nov 2006 17:54:51 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 28 Nov 2006 17:54:51 -0000
Received: (qmail 23351 invoked by alias); 28 Nov 2006 17:54:39 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 11047
Received: (qmail 23342 invoked from network); 28 Nov 2006 17:54:38 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 28 Nov 2006 17:54:38 -0000
Received: (qmail 44004 invoked from network); 28 Nov 2006 17:54:38 -0000
Received: from cluster-d.mailcontrol.com (217.69.20.190)
  by a.mx.sunsite.dk with SMTP; 28 Nov 2006 17:54:34 -0000
Received: from cameurexb01.EUROPE.ROOT.PRI ([62.189.241.200])
	by rly03d.srv.mailcontrol.com (MailControl) with ESMTP id kASHsVqE004453
	for <zsh-users@sunsite.dk>; Tue, 28 Nov 2006 17:54:31 GMT
Received: from news01.csr.com ([10.103.143.38]) by cameurexb01.EUROPE.ROOT.PRI with Microsoft SMTPSVC(6.0.3790.1830);
	 Tue, 28 Nov 2006 17:54:29 +0000
Received: from news01.csr.com (localhost.localdomain [127.0.0.1])
	by news01.csr.com (8.13.8/8.13.4) with ESMTP id kASHsTgK020563
	for <zsh-users@sunsite.dk>; Tue, 28 Nov 2006 17:54:29 GMT
Received: from csr.com (pws@localhost)
	by news01.csr.com (8.13.8/8.13.8/Submit) with ESMTP id kASHsSGa020560
	for <zsh-users@sunsite.dk>; Tue, 28 Nov 2006 17:54:29 GMT
Message-Id: <200611281754.kASHsSGa020560@news01.csr.com>
X-Authentication-Warning: news01.csr.com: pws owned process doing -bs
To: zsh-users@sunsite.dk (Zsh users list)
Subject: Zle function for moving list elements
Date: Tue, 28 Nov 2006 17:54:28 +0000
From: Peter Stephenson <pws@csr.com>
X-OriginalArrivalTime: 28 Nov 2006 17:54:29.0912 (UTC) FILETIME=[40B46180:01C71316]
Content-Type: text/plain
MIME-Version: 1.0
X-Scanned-By: MailControl A-07-06-80 (www.mailcontrol.com) on 10.68.0.113

Here's a function move-element I've just written to move an element in a
colon- or comma-separated list in the shell's editor up or down.  This
makes it very easy to reorder a PATH in vared, or whatever.  You can
tweak the separator characters by changing the "local chars=" assignment
if you like (you might not want IFS characters in it).

autoload -U move-element
zle -N move-element move-element-backward
zle -N move-element move-element-forward


#start
# Take a list like foo=prev:blah:next or foo=prev,blah,next or even without
# the foo= and move the current element backward (foo=blah,prev,next)
# or forward (foo=prev,next,blah)

emulate -L zsh

local chars='[:IFS:],:='
integer curs=$CURSOR
integer elstart=$((CURSOR+1)) elend=$((CURSOR+1))
while [[ $elstart -gt 1 && $BUFFER[$elstart-1] != [${~chars}] ]]; do
  (( elstart-- ))
done
while [[ $elend -lt ${#BUFFER} && $BUFFER[$elend+1] != [${~chars}] ]]; do
  (( elend++ ))
done

integer inspos
if [[ $WIDGET = *forward* ]]; then
  (( inspos = elend + 1))
  while [[ $inspos -lt ${#BUFFER} && $BUFFER[$inspos+1] != [${~chars}] ]]; do
    (( inspos++ ))
  done

  # Insert first to keep the indexes the same
  BUFFER[$inspos+1]=\
"$BUFFER[$elend+1]$BUFFER[$elstart,$elend]$BUFFER[$inspos+1]"
  BUFFER[$elstart,$elend+1]=
  (( CURSOR = curs + inspos - elend ))
else
  (( inspos = elstart - 1 ))
  while [[ $inspos -gt 1 && $BUFFER[$inspos-1] != [${~chars}] ]]; do
    (( inspos-- ))
  done

  # Delete first to keep the indexes the same
  local sep=$BUFFER[$elstart-1] str=$BUFFER[$elstart,$elend]
  BUFFER[$elstart-1,$elend]=
  BUFFER[$inspos]="$str$sep$BUFFER[$inspos]"
  (( CURSOR = curs - (elstart - inspos) ))
fi
#end

-- 
Peter Stephenson <pws@csr.com>                  Software Engineer
CSR PLC, Churchill House, Cambridge Business Park, Cowley Road
Cambridge, CB4 0WZ, UK                          Tel: +44 (0)1223 692070


To access the latest news from CSR copy this link into a web browser:  http://www.csr.com/email_sig.php

