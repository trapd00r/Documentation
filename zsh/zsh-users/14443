From zsh-users-return-14443-mason-zsh=primenet.com.au@sunsite.dk Wed Sep 30 19:54:37 2009
Return-Path: <zsh-users-return-14443-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 3352 invoked from network); 30 Sep 2009 19:54:24 -0000
X-Spam-Checker-Version: SpamAssassin 3.2.5 (2008-06-10) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.6 required=5.0 tests=AWL,BAYES_00 autolearn=ham
	version=3.2.5
Received: from new-brage.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.254.104)
  by ns1.primenet.com.au with SMTP; 30 Sep 2009 19:54:24 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 81966 invoked from network); 30 Sep 2009 19:54:08 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 30 Sep 2009 19:54:08 -0000
Received: (qmail 13839 invoked by alias); 30 Sep 2009 19:53:55 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 14443
Received: (qmail 13820 invoked from network); 30 Sep 2009 19:53:54 -0000
Received: from bifrost.dotsrc.org (130.225.254.106)
  by sunsite.dk with SMTP; 30 Sep 2009 19:53:54 -0000
Received: from mtaout02-winn.ispmail.ntl.com (mtaout02-winn.ispmail.ntl.com [81.103.221.48])
	by bifrost.dotsrc.org (Postfix) with ESMTP id F2EFD801E2BF
	for <zsh-users@sunsite.dk>; Wed, 30 Sep 2009 21:53:45 +0200 (CEST)
Received: from aamtaout02-winn.ispmail.ntl.com ([81.103.221.35])
          by mtaout02-winn.ispmail.ntl.com
          (InterMail vM.7.08.04.00 201-2186-134-20080326) with ESMTP
          id <20090930195343.FJUE27507.mtaout02-winn.ispmail.ntl.com@aamtaout02-winn.ispmail.ntl.com>
          for <zsh-users@sunsite.dk>; Wed, 30 Sep 2009 20:53:43 +0100
Received: from pws-pc ([82.6.98.90]) by aamtaout02-winn.ispmail.ntl.com
          (InterMail vG.2.02.00.01 201-2161-120-102-20060912) with ESMTP
          id <20090930195343.OMWO21638.aamtaout02-winn.ispmail.ntl.com@pws-pc>
          for <zsh-users@sunsite.dk>; Wed, 30 Sep 2009 20:53:43 +0100
Date: Wed, 30 Sep 2009 20:53:39 +0100
From: Peter Stephenson <p.w.stephenson@ntlworld.com>
To: zsh-users@sunsite.dk
Subject: Re: matlab-history-beginning-search-backward
Message-ID: <20090930205339.7dbad268@pws-pc>
In-Reply-To: <140222.25175.qm@web65614.mail.ac4.yahoo.com>
References: <140222.25175.qm@web65614.mail.ac4.yahoo.com>
X-Mailer: Claws Mail 3.7.2 (GTK+ 2.16.6; x86_64-redhat-linux-gnu)
Mime-Version: 1.0
Content-Type: text/plain; charset=US-ASCII
Content-Transfer-Encoding: 7bit
X-Cloudmark-Analysis: v=1.0 c=1 a=uBAoy4U1U-sA:10 a=CjxXgO3LAAAA:8 a=Rx4RxmsOsmFSOtAEaW4A:9 a=smer0eCaDTi2BVTfo5VaBZjSJg0A:4 a=rC2wZJ5BpNYA:10
X-Virus-Scanned: ClamAV 0.94.2/9856/Wed Sep 30 14:27:30 2009 on bifrost
X-Virus-Status: Clean

On Wed, 30 Sep 2009 09:52:10 -0700 (PDT)
Guido van Steen <gvsteen@yahoo.com> wrote:
> I use the "history-beginning-search-backward" widget quite
> frequently. It searches for commands in the command history that START
> WITH "LBUFFER". 
>
> I like this behaviour. It is simple and it keeps my attention fixed on
> one line. What I don't like about the widget is that ONLY searches for
> commands that start with "LBUFFER". Instead I would prefer a similar
> widget that searches for commands in the command history, in such a way
> that "LBUFFER" is a SUBSTRING of these commands.

(I moved this to zsh-users.)

This seemed to work.  I stole a lot of it from
history-beginning-search-menu.  It implements forward and backward
searches, so you can do something like

zle -N history-substring-search-forward history-substring-search-backward
zle -N history-substring-search-backward
bindkey '\ep' history-substring-search-backward
bindkey '\en' history-substring-search-forward

At this point, it is traditional for people to say it's absolutely
crucial that the cursor be posititioned etc. etc...

pws


# history-substring-search-backward
# can also be used for the widget history-substring-search-forward.

emulate -L zsh
setopt extendedglob

zmodload -i zsh/parameter

local search=$LBUFFER MATCH MBEGIN MEND

typeset -g ZLE_HISTORY_SUBSTRING_MATCH

if [[ $LASTWIDGET = history-substring-search-* ]]; then
  # here's one I prepared earlier
  search=$ZLE_HISTORY_SUBSTRING_MATCH
else
  # We need to quote metacharacters in the search string
  # since they are otherwise active in the reverse subscript.
  # We need to avoid quoting other characters since they aren't
  # and just stay quoted, rather annoyingly.
  search=${LBUFFER//(#m)[\][()\\*?#<>~^]/\\$MATCH}
  ZLE_HISTORY_SUBSTRING_MATCH=$search
fi

local -aU matches
matches=(${(kon)history[(R)*${search}*]})

# Filter out any match that's the same as the original.
# Note this isn't a pattern this time.
matches=(${matches:#${LBUFFER}})

(( ${#matches} )) || return 1

if [[ $WIDGET = *forward* ]]; then
  eval "matches=(\${matches:#<-$HISTNO>})"
  (( ${#matches} )) || return 1
  HISTNO=${matches[1]}
else
  eval "matches=(\${matches:#<$HISTNO->})"
  (( ${#matches} )) || return 1
  HISTNO=${matches[-1]}
fi

