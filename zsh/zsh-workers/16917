From zsh-workers-return-16917-mason-zsh=primenet.com.au@sunsite.dk Wed Mar 27 19:36:08 2002
Return-Path: <zsh-workers-return-16917-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 12364 invoked from network); 27 Mar 2002 19:36:07 -0000
Received: from sunsite.dk (130.225.247.90)
  by ns1.primenet.com.au with SMTP; 27 Mar 2002 19:36:07 -0000
Received: (qmail 16868 invoked by alias); 27 Mar 2002 19:35:58 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 16917
Received: (qmail 16856 invoked from network); 27 Mar 2002 19:35:57 -0000
X-VirusChecked: Checked
Date: Wed, 27 Mar 2002 19:35:28 +0000
From: Oliver Kiddle <okiddle@yahoo.co.uk>
To: Bart Schaefer <schaefer@brasslantern.com>
Cc: zsh-workers@sunsite.dk
Subject: Re: up-line-or-search question
Message-ID: <20020327193528.GA6803@logica.com>
References: <20020327180029.GA20166@logica.com> <Pine.LNX.4.44.0203271004020.17952-100000@ns1.sodaware.com>
Mime-Version: 1.0
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline
In-Reply-To: <Pine.LNX.4.44.0203271004020.17952-100000@ns1.sodaware.com>
User-Agent: Mutt/1.3.27i
Sender: Oliver Kiddle <kiddleo@logica.com>

On Wed, Mar 27, 2002 at 10:19:23AM -0800, Bart Schaefer wrote:
> 
> Search up successfully, and then immediately try to search down again?

That now works. I probably had something messed up yesterday.

> E.g., change :predict to :zle:predict ?

Exactly.

> I wish we'd thought about that sooner ... it's not a bad idea, but it
> means changing predict-on, incremental-complete-word, etc. ... maybe
> we can use the excuse of differentiating complex stateful widgets from
> simple zle functions.

I would be inclined to change them anyway but if it worries you, we could
use something like:

zstyle -t :zle:predict verbose || zle -t :predict verbose

I've put the functions below in a recent form but without some of my
weirder additions. Suggestions on suitable shortenings for the style
contexts welcome. Or any other changes?

I've also added another bit to the up-line function which causes it
to still do an up-history if the search failed but we are not
currently in a search. This allows it to still be clear when a
search reaches it's end but it will still go up when searching isn't
going to do anything. Any views on this? Perhaps it also needs
a style. Note that a corresponding change to the down function
would be needed.

Oliver

up-line-or-beginning-search() {
  [[ $LASTWIDGET != $__searching ]]
  local insearch=$?

  if [[ $LBUFFER == *$'\n'* ]]; then
    zle .up-line-or-history
    __searching=''
  elif [[ -n $PREBUFFER ]] && 
      zstyle -t ':zle:up-line-or-beginning-search' edit-buffer
  then
    zle .push-line-or-edit
  else
    (( insearch )) && CURSOR=$__savecursor
    __savecursor=$CURSOR
    __searching=$WIDGET
    if ! zle .history-beginning-search-backward && (( ! insearch )); then
      zle .up-history
      CURSOR=0
    fi
    zstyle -T ':zle:up-line-or-beginning-search' leave-cursor &&
        zle .end-of-line
  fi
}

down-line-or-beginning-search() {
  if [[ ${+NUMERIC} -eq 0 &&
      ( $LASTWIDGET = $__searching || $RBUFFER != *$'\n'* ) ]]
  then
    [[ $LASTWIDGET = $__searching ]] && CURSOR=$__savecursor
    __searching=$WIDGET
    __savecursor=$CURSOR
    if zle .history-beginning-search-forward; then
      [[ $RBUFFER = *$'\n'* ]] || 
          zstyle -T ':zle:down-line-or-beginning-search' leave-cursor &&
	  zle .end-of-line
      return
    fi
    [[ $RBUFFER = *$'\n'* ]] || return
  fi
  __searching=''
  zle .down-line-or-history
}

This e-mail and any attachment is for authorised use by the intended recipient(s) only.  It may contain proprietary material, confidential information and/or be subject to legal privilege.  It should not be copied, disclosed to, retained or used by, any other party.  If you are not an intended recipient then please promptly delete this e-mail and any attachment and all copies and inform the sender.  Thank you.

