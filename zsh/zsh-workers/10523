From zsh-workers-return-10523-mason-zsh=primenet.com.au@sunsite.auc.dk Wed Apr 05 19:22:04 2000
Return-Path: <zsh-workers-return-10523-mason-zsh=primenet.com.au@sunsite.auc.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 1007 invoked from network); 5 Apr 2000 19:22:02 -0000
Received: from sunsite.auc.dk (130.225.51.30)
  by ns1.primenet.com.au with SMTP; 5 Apr 2000 19:22:02 -0000
Received: (qmail 21938 invoked by alias); 5 Apr 2000 19:21:51 -0000
Mailing-List: contact zsh-workers-help@sunsite.auc.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.auc.dk
X-Seq: 10523
Received: (qmail 21922 invoked from network); 5 Apr 2000 19:21:51 -0000
To: Zsh workers <zsh-workers@sunsite.auc.dk>
Subject: Re: PATCH: AIX dep.&doc fix; development guidelines 
In-reply-to: ""Bart Schaefer""'s message of "Wed, 05 Apr 2000 18:06:39 -0000."
             <1000405180640.ZM15161@candle.brasslantern.com> 
Date: Wed, 05 Apr 2000 20:21:36 +0100
From: Peter Stephenson <pws@pwstephenson.fsnet.co.uk>
Message-Id: <E12cvMq-00036h-00.2000-04-05-20-21-33@cmailg7.svr.pol.co.uk>

"Bart Schaefer" wrote:
> Peter wants each developer to create his own.  This causes some oddities
> e.g. when Sven and I were both patching today -- article number refs get
> out of order, and I had to restart my commit because Sven sneaked one in
> between my "cvs update" and finishing writing the log message.

I'm happy to consider other ways, but this way we get the changes properly
documented up to date.  If anyone has a script to generate them in
something like the current format, with applier, patch number, files
changed, and brief description, from CVS data when it is committed that
would be splendid.

> } 3. The .distfiles thing should be mentioned. I take it that we just add
> } and remove files from the list in it if we add and remove files?
> 
> That's correct.

There's a few files that you might of thought would be in the repository
but are actually auto-generated --- this is basically Zefram's preconfig
mechanism.  For everything else, they just go in or out of .distfiles as
appropriate.

Here's a script I use for checking for thinks that aren't in either
.distfiles as a source file or .cvsignore.  It doesn't examine
DISTFILES_DOC at the moment.  It can go in /home/groups/zsh/bin at
sourceforge.  It told me that LICENCE hadn't been added, so that will go
into .distfiles when I next commit.


#!/bin/zsh -f

emulate zsh
setopt nonomatch nobadpattern

typeset -A sfiles

# srcdir=~/zsh/sourceforge/zsh
srcdir=/home/groups/zsh/zsh

if [[ ! -d $srcdir ]]; then
  print "Can't find top of distribution" >&2
  exit 1
fi

srcdir=$srcdir[1]

cd $srcdir

typeset -A fdone

for dir in '' **/*(/); do
  if [[ -n $dir ]]; then
    cd $srcdir/$dir
    dir="$dir/"
  else
    cd $srcdir
  fi
  unset DISTFILES_SRC
  fdone=()
  if [[ -f .distfiles ]]; then
    . ./.distfiles
    if (( ${+DISTFILES_SRC} )); then
      for f in ${~=DISTFILES_SRC}; do
	[[ "$dir$f" = *Doc/*.1 || "$dir$f" = *.texi ]] && continue
# the following lists the files to be added to the archive
#       print "$dir$f"
	[[ -f $f ]] || print "WARNING: no such file $dir$f" >&2
        fdone[$f]=1
      done
    else
      print "WARNING: ${dir}.distfiles didn't set DISTFILES_SRC" >&2
    fi
    if [[ -f .cvsignore ]]; then
      for f in ${~$(<.cvsignore)}; do
	[[ -f $f ]] && fdone[$f]=1
      done
    fi
    for f in *; do
      [[ -d $f ]] && continue
      [[ -z $fdone[$f] ]] && print "WARNING: file $dir$f not handled" >&2
    done
  elif [[ $PWD = */CVS ]]; then
    continue
  else
    print "WARNING: ${dir:-top level directory} has no .distfiles" >&2
  fi
done

exit 0

-- 
Peter Stephenson <pws@pwstephenson.fsnet.co.uk>
Work: pws@CambridgeSiliconRadio.com
Web: http://www.pwstephenson.fsnet.co.uk

