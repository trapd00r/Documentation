From zsh-workers-return-22940-mason-zsh=primenet.com.au@sunsite.dk Wed Nov 01 03:02:06 2006
Return-Path: <zsh-workers-return-22940-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 18854 invoked from network); 1 Nov 2006 03:02:02 -0000
X-Spam-Checker-Version: SpamAssassin 3.1.7 (2006-10-05) on f.primenet.com.au
X-Spam-Level: 
X-Spam-Status: No, score=-2.5 required=5.0 tests=AWL,BAYES_00,
	FORGED_RCVD_HELO autolearn=ham version=3.1.7
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 1 Nov 2006 03:02:02 -0000
Received-SPF: none (ns1.primenet.com.au: domain at sunsite.dk does not designate permitted sender hosts)
Received: (qmail 37401 invoked from network); 1 Nov 2006 03:01:55 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 1 Nov 2006 03:01:55 -0000
Received: (qmail 17913 invoked by alias); 1 Nov 2006 03:01:51 -0000
Mailing-List: contact zsh-workers-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-workers@sunsite.dk
X-Seq: 22940
Received: (qmail 17903 invoked from network); 1 Nov 2006 03:01:50 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 1 Nov 2006 03:01:50 -0000
Received: (qmail 37133 invoked from network); 1 Nov 2006 03:01:50 -0000
Received: from acolyte.scowler.net (216.254.112.45)
  by a.mx.sunsite.dk with SMTP; 1 Nov 2006 03:01:49 -0000
Received: by acolyte.scowler.net (Postfix, from userid 1000)
	id 5894770055; Tue, 31 Oct 2006 22:01:45 -0500 (EST)
Date: Tue, 31 Oct 2006 22:01:45 -0500
From: Clint Adams <clint@zsh.org>
To: zsh-workers@sunsite.dk
Subject: PATCH: _mkdir
Message-ID: <20061101030145.GA31646@scowler.net>
Mail-Followup-To: zsh-workers@sunsite.dk
MIME-Version: 1.0
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline
User-Agent: Mutt/1.5.13 (2006-08-11)

Another from R.Ramkumar.

Index: Completion/Unix/Command/_mkdir
===================================================================
RCS file: Completion/Unix/Command/_mkdir
diff -N Completion/Unix/Command/_mkdir
--- /dev/null	1 Jan 1970 00:00:00 -0000
+++ Completion/Unix/Command/_mkdir	1 Nov 2006 03:00:02 -0000
@@ -0,0 +1,70 @@
+#compdef mkdir
+
+local curcontext="$curcontext" line state \
+  args args_zsh args_cmd variant expl ret=1
+typeset -a opt_args
+
+args=(
+  '(-m --mode=)'{-m,--mode=}'[set permission mode]:numeric mode'
+  '(-p --parents)'{-p,--parents}'[make parent directories as needed]'
+  )
+
+args_zsh=('(-)*: :->dir')
+
+args_cmd=(
+  '(-v --verbose)'{-v,--verbose}'[print message for each created directory]'
+  '(- :)--help[display help information]'
+  '(- :)--version[display version information]'
+  '*: :->dir'
+  )
+
+case "$OSTYPE" in
+  linux*)
+    args_cmd=(
+      '(-Z --context=)'{-Z,--context=}'[set SELinux context]:SELinux context'
+      $args_cmd)
+    ;;
+esac
+
+_pick_variant -r variant gnu=gnu zsh='\(eval\)' unix --help
+
+# It can still happen that there is a precommand command or builtin in the line.
+# In such cases, the variant has to be modified suitably, after further checking
+# the variant of the _command_ mkdir.
+
+# I currently don't know of any way to find out what precommands are present on
+# the line. The variant should be modified like this once a way is found out:
+
+# if [[ $variant == zsh ]]; then
+#   if [[ $precommand = *command* ]]; then
+#     _mkdir_command () { command mkdir "$@" }
+#     _pick_variant -c _mkdir_command -r variant gnu=gnu unix --help
+#   fi
+# elif [[ $precommand = *builtin* ]]; then
+#   variant=zsh
+# fi
+
+if [[ $variant == zsh ]]; then
+  args+=($args_zsh)
+else
+  args+=($args_cmd)
+fi
+
+# remove long options?
+[[ $variant != gnu ]] && args=( ${${${args:#(|*\))--*}//--[^ )]#/}/\( #\)/} )
+
+_arguments -C -s $args && ret=0
+
+case "$state" in
+  dir)
+    if (( $ret )) && [[ ! -prefix - ]] || \
+      [[ $variant == zsh && ${#${${words[2,-1]}:#-*}} -gt 0 ]]; then
+      _wanted parent-directory expl \
+	'parent directory (alternatively specify name of directory)' \
+	_path_files -/ || _message 'name of directory'
+      ret=0
+    fi
+    ;;
+esac
+
+return ret

