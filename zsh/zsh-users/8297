From zsh-users-return-8297-mason-zsh=primenet.com.au@sunsite.dk Sun Dec 12 20:11:07 2004
Return-Path: <zsh-users-return-8297-mason-zsh=primenet.com.au@sunsite.dk>
Delivered-To: mason-zsh@primenet.com.au
Received: (qmail 6530 invoked from network); 12 Dec 2004 20:11:05 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by ns1.primenet.com.au with SMTP; 12 Dec 2004 20:11:05 -0000
Received: (qmail 14770 invoked from network); 12 Dec 2004 20:10:59 -0000
Received: from sunsite.dk (130.225.247.90)
  by a.mx.sunsite.dk with SMTP; 12 Dec 2004 20:10:59 -0000
Received: (qmail 572 invoked by alias); 12 Dec 2004 20:10:08 -0000
Mailing-List: contact zsh-users-help@sunsite.dk; run by ezmlm
Precedence: bulk
X-No-Archive: yes
Delivered-To: mailing list zsh-users@sunsite.dk
X-Seq: 8297
Received: (qmail 558 invoked from network); 12 Dec 2004 20:10:08 -0000
Received: from news.dotsrc.org (HELO a.mx.sunsite.dk) (130.225.247.88)
  by sunsite.dk with SMTP; 12 Dec 2004 20:10:08 -0000
Received: (qmail 13500 invoked from network); 12 Dec 2004 20:10:08 -0000
Received: from mxfep02.bredband.com (195.54.107.73)
  by a.mx.sunsite.dk with SMTP; 12 Dec 2004 20:10:07 -0000
Received: from puritan.pcp.ath.cx ([213.112.43.197] [213.112.43.197])
          by mxfep02.bredband.com with ESMTP
          id <20041212201007.DYKO6820.mxfep02.bredband.com@puritan.pcp.ath.cx>
          for <zsh-users@sunsite.dk>; Sun, 12 Dec 2004 21:10:07 +0100
Received: by puritan.pcp.ath.cx (Postfix, from userid 1000)
	id 0F32CAE044; Sun, 12 Dec 2004 21:10:08 +0100 (CET)
Date: Sun, 12 Dec 2004 21:10:08 +0100
From: Nikolai Weibull <mailing-lists.zsh-users@rawuncut.elitemail.org>
To: zsh-users@sunsite.dk
Subject: Re: Vim syntax file for zsh
Message-ID: <20041212201008.GB9693@puritan.pcp.ath.cx>
Mail-Followup-To: zsh-users@sunsite.dk
References: <dc507f4a04120908364a032da6@mail.gmail.com> <20041209181813.GF9424@puritan.pcp.ath.cx> <20041209182347.GG9424@puritan.pcp.ath.cx> <20041209200941.GA27510@blorf.net> <20041209212423.GA21023@puritan.pcp.ath.cx> <20041209224315.GB27510@blorf.net> <20041209232655.GB21023@puritan.pcp.ath.cx> <dc507f4a04121120414ae83b7f@mail.gmail.com> <20041212133409.GA9431@puritan.pcp.ath.cx> <dc507f4a041212084069f6e782@mail.gmail.com>
Mime-Version: 1.0
Content-Type: multipart/mixed; boundary="mYCpIKhGyMATD0i+"
Content-Disposition: inline
In-Reply-To: <dc507f4a041212084069f6e782@mail.gmail.com>
User-Agent: Mutt/1.5.6i
X-Spam-Checker-Version: SpamAssassin 2.63 on a.mx.sunsite.dk
X-Spam-Level: ****
X-Spam-Status: No, hits=4.1 required=6.0 tests=OPT_IN_CAPS,RCVD_IN_SORBS 
	autolearn=no version=2.63
X-Spam-Hits: 4.1


--mYCpIKhGyMATD0i+
Content-Type: text/plain; charset=us-ascii
Content-Disposition: inline

* Felix Rosencrantz <f.rosencrantz@gmail.com> [Dec 12, 2004 17:50]:
> > Suggestions?  I guess that if there was a good way of matching stuff like
>        ${var/pat/sub}
>             ^^^^^^^^
> Isn't this the same problem as a finding a quoted string?  In this
> case it has to follow a variable name within braces...  Though maybe I
> don't understand.

This should be fixed now...I made something complex when it should have
been simple.

> Also the here-strings formatting ( which is different from here-docs
> <<) is  wrong: cat <<<"This is a here string" should highlight the
> string somehow. not just the first word.   It seems confused by the
> syntax, because of the closing ".

Ah, switching order of redirections and heredoc definitions worked fine.

> It's up to you about some well-known functions.  

We'll leave them out for now.

> It would be useful if there was a file or set of files that
> demonstrate syntactic features of zsh that could be used as a test
> case for the syntax highlighting.

Yeah, that would help :-),
	nikolai

-- 
::: name: Nikolai Weibull    :: aliases: pcp / lone-star / aka :::
::: born: Chicago, IL USA    :: loc atm: Gothenburg, Sweden    :::
::: page: www.pcppopper.org  :: fun atm: gf,lps,ruby,lisp,war3 :::
main(){printf(&linux["\021%six\012\0"],(linux)["have"]+"fun"-97);}

--mYCpIKhGyMATD0i+
Content-Type: text/plain; charset=us-ascii
Content-Disposition: attachment; filename="zsh.vim"

" Vim syntax file
" Language:	    Zsh shell script
" Maintainer:	    Nikolai Weibull <source@pcppopper.org>
" URL:		    http://www.pcppopper.org/vim/syntax/pcp/zsh/
" Latest Revision:  2004-12-12
" arch-tag:	    2e2c7097-99cb-4b87-a771-3a819b69995e

if version < 600
  syntax clear
elseif exists("b:current_syntax")
  finish
endif

" Set iskeyword since we need `-' (and potentially others) in keywords.
" For version 5.x: Set it globally
" For version 6.x: Set it locally
if version >= 600
  command -nargs=1 SetIsk setlocal iskeyword=<args>
else
  command -nargs=1 SetIsk set iskeyword=<args>
endif
SetIsk @,48-57,_,-
delcommand SetIsk

" Todo
syn keyword zshTodo         contained TODO FIXME XXX NOTE

" Comments
syn region  zshComment      matchgroup=zshComment start='\%(^\|\s\)#' end='$' contains=zshTodo

" PreProc
syn match   zshPreProc	    '^\%1l#\%(!\|compdef\|autoload\).*$'

" Strings
syn match   zshQuoted       '\\.'
syn region  zshString       matchgroup=zshStringDelimiter start=+"+ end=+"+ contains=zshQuoted,@zshDerefs,@zshSubst
syn region  zshString       matchgroup=zshStringDelimiter start=+'+ end=+'+
" XXX: This should probably be more precise, but Zsh seems a bit confused about it itself
syn region  zshPOSIXString  matchgroup=zshStringDelimiter start=+\$'+ end=+'+ contains=zshQuoted
syn match   zshJobSpec      '%\(\d\+\|?\=\w\+\|[%+-]\)'

" Precommand Modifiers
syn keyword zshPrecommand   noglob nocorrect exec command builtin - time

" Delimiters
syn keyword zshDelimiter    do done

" Conditionals
syn keyword zshConditional  if then elif else fi case in esac select

" Loops
syn keyword zshRepeat       for while until repeat foreach

" Exceptions
syn keyword zshException    always

" Keywords
syn keyword zshKeyword      function nextgroup=zshKSHFunction skipwhite

" Functions
syn match   zshKSHFunction  contained '\k\+'
syn match   zshFunction     '^\s*\k\+\ze\s*()'

" Operators
syn match   zshOperator	    '||\|&&\|;\|&!\='

" Here Documents
if version < 600
  " Do nothing for now TODO: do something
else
  syn region  zshHereDoc  matchgroup=zshRedir start='<<\s*\z(\S*\)' end='^\z1$' contains=@zshSubst
  syn region  zshHereDoc  matchgroup=zshRedir start='<<-\s*\z(\S*\)' end='^\s*\z1$' contains=@zshSubst
  syn region  zshHereDoc  matchgroup=zshRedir start='<<\s*\(["']\)\z(\S*\)\1'  end='^\z1$'
  syn region  zshHereDoc  matchgroup=zshRedir start='<<-\s*\(["']\)\z(\S*\)\1' end='^\s*\z1$'
endif

" Redirections
syn match   zshRedir        '\d\=\(<\|<>\|<<<\|<&\s*[0-9p-]\=\)'
syn match   zshRedir        '\d\=\(>\|>>\|>&\s*[0-9p-]\=\|&>\|>>&\|&>>\)[|!]\='
syn match   zshRedir        '|&\='

" Variable Assignments
syn match   zshVariable	    '\<\h\w*\ze+\=='
" XXX: how safe is this?
syn region  zshVariable	    oneline matchgroup=zshVariable start='\$\@<!\<\h\w*\[' end='\]\ze+\==' contains=@zshSubst

" Variable Dereferencing
syn cluster zshDerefs contains=zshShortDeref,zshLongDeref,zshDeref

if !exists("g:zsh_syntax_variables")
  let s:zsh_syntax_variables = 'all'
else
  let s:zsh_syntax_variables = g:zsh_syntax_variables
endif

syn match zshShortDeref   '\$[!#$*@?_-]\w\@!'
syn match zshShortDeref   '\$[=^~]*[#+]*\d\+\>'

syn match zshLongDeref    '\$\%(ARGC\|argv\|status\|pipestatus\|CPUTYPE\|EGID\|EUID\|ERRNO\|GID\|HOST\|LINENO\|LOGNAME\)'
syn match zshLongDeref    '\$\%(MACHTYPE\|OLDPWD OPTARG\|OPTIND\|OSTYPE\|PPID\|PWD\|RANDOM\|SECONDS\|SHLVL\|signals\)'
syn match zshLongDeref    '\$\%(TRY_BLOCK_ERROR\|TTY\|TTYIDLE\|UID\|USERNAME\|VENDOR\|ZSH_NAME\|ZSH_VERSION\|REPLY\|reply\|TERM\)'

syn match zshDeref	  '\$[=^~]*[#+]*\h\w*\>'

" Commands
syn match   zshCommands     '\%(^\|\s\)[.:]\ze\s'
syn keyword zshCommands     alias autoload bg bindkey break bye cap cd chdir
syn keyword zshCommands     clone comparguments compcall compctl compdescribe
syn keyword zshCommands     compfiles compgroups compquote comptags comptry
syn keyword zshCommands     compvalues continue declare dirs disable disown
syn keyword zshCommands     echo echotc echoti emulate enable eval exec exit
syn keyword zshCommands     export false fc fg functions getcap getln
syn keyword zshCommands     getopts hash history jobs kill let limit
syn keyword zshCommands     log logout popd print printf pushd pushln
syn keyword zshCommands     pwd r read readonly rehash return sched set
syn keyword zshCommands     setcap setopt shift source stat suspend test times
syn keyword zshCommands     trap true ttyctl type ulimit umask unalias
syn keyword zshCommands     unfunction unhash unlimit unset unsetopt vared
syn keyword zshCommands     wait whence where which zcompile zformat zftp zle
syn keyword zshCommands     zmodload zparseopts zprof zpty zregexparse zsocket
syn keyword zshCommands     zstyle ztcp

" Types
syn keyword zshTypes        float integer local typeset

" Switches
" XXX: this may be too much
syn match   zshSwitches     '\s\zs--\=[a-zA-Z0-9-]\+'

" Numbers
syn match   zshNumber	    '[-+]\=0x\x\+\>'
syn match   zshNumber	    '[-+]\=0\o\+\>'
syn match   zshNumber	    '[-+]\=\d\+#[-+]\=\w\+\>'
syn match   zshNumber	    '[-+]\=\d\+\.\d\+\>'

" Substitution
syn cluster zshSubst	    contains=zshSubst,zshOldSubst
syn region  zshSubst        matchgroup=zshSubstDelim transparent start='\$(' skip='\\)' end=')' contains=TOP
syn region  zshSubst        matchgroup=zshSubstDelim transparent start='\$((' skip='\\)' end='))' contains=TOP
syn region  zshSubst	    matchgroup=zshSubstDelim start='\${' skip='\\}' end='}' contains=@zshSubst
syn region  zshOldSubst	    matchgroup=zshSubstDelim start=+`+ skip=+\\`+ end=+`+ contains=TOP,zshOldSubst

" Define the default highlighting.
" For version 5.7 and earlier: only when not done already
" For version 5.8 and later: only when an item doesn't have highlighting yet
if version >= 508 || !exists("did_zsh_syn_inits")
  if version < 508
    let did_zsh_syn_inits = 1
    command -nargs=+ HiLink hi link <args>
  else
    command -nargs=+ HiLink hi def link <args>
  endif

  HiLink zshTodo	    Todo
  HiLink zshComment	    Comment
  HiLink zshPreProc	    PreProc
  HiLink zshQuoted	    SpecialChar
  HiLink zshString	    String
  HiLink zshStringDelimiter zshString
  HiLink zshPOSIXString	    zshString
  HiLink zshJobSpec	    Special
  HiLink zshPrecommand	    Special
  HiLink zshDelimiter	    Keyword
  HiLink zshConditional	    Conditional
  HiLink zshException	    Exception
  HiLink zshRepeat	    Repeat
  HiLink zshKeyword	    Keyword
  HiLink zshFunction	    Function
  HiLink zshKSHFunction	    zshFunction
  HiLink zshHereDoc	    String
  HiLink zshOperator	    Operator
  HiLink zshRedir	    Operator
  HiLink zshVariable	    Identifier
  HiLink zshDereferencing   PreProc
  if s:zsh_syntax_variables =~ 'short\|all'
    HiLink zshShortDeref    zshDereferencing
  else
    HiLink zshShortDeref    None
  endif
  if s:zsh_syntax_variables =~ 'long\|all'
    HiLink zshLongDeref	    zshDereferencing
  else
    HiLink zshLongDeref    None
  endif
  if s:zsh_syntax_variables =~ 'all'
    HiLink zshDeref	    zshDereferencing
  else
    HiLink zshDerefDeref    None
  endif
  HiLink zshCommands	    Keyword
  HiLink zshTypes	    Type
  HiLink zshSwitches	    Special
  HiLink zshNumber	    Number
  HiLink zshSubst	    PreProc
  HiLink zshOldSubst	    zshSubst
  HiLink zshSubstDelim	    zshSubst

  delcommand HiLink
endif

let b:current_syntax = "zsh"

" vim: set sts=2 sw=2:

--mYCpIKhGyMATD0i+--

