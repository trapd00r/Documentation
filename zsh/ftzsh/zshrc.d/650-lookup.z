### vim:ft=zsh:foldmethod=marker

if ! (( ${+functions[lookup]} )); then
    zprintf 2 'Lookup is not in use, skipping setup.\n'
    return 0
fi

zstyle ':lookup:*'         use-pager   true
zstyle ':lookup:*'         pager-auto  true
zstyle ':lookup:*'         pager       'less -M'
zstyle ':lookup:*'         txt-browser 'w3m'
zstyle ':lookup:*'         txt-formats '%s'
zstyle ':lookup:*:rfc:*'   search-path ~/share/rfc /usr/share/doc/RFC/links
zstyle ':lookup:*:rfc:*'   save-path   ~/share/rfc

zstyle -e ':lookup:*' gui-browser '
    typeset -ga reply
    if pgrep -f -u ${EUID} google-chrome >/dev/null; then
        reply=(google-chrome)
    else
        reply=(iceweasel)
    fi'
zstyle -e ':lookup:*' gui-formats '
    typeset -ga reply
    if pgrep -f -u ${EUID} google-chrome >/dev/null; then
        reply=(%s)
    else
        reply=(-remote "openurl(%s)")
    fi'

# Here's a nice one: I got this script cmus-rstat.sh, which tells me which
# track cmus (my audio player) is currently playing in this format:
#       np: Artist - Album - Track
#
# Well, that's easily parsable. :)
# Now I can make automated searches via letssingit.com with lookup's
# query-handler feature like this:
#
# If I want to search for the artist:
#   % lookup -Q letssingit -m artist
# or the album:
#   % lookup -Q letssingit -m album
# or the track name (for instant sing-along :->):
#   % lookup -Q letssingit -m song
#
zstyle ':lookup:*:letssingit:*' query-handlers letssingit
function LOOKUP_qh_letssingit() {
    local mode=${lookup_communicate[mode]}
    local np

    case ${mode} in
    (artist|album|song)
        np="$(cmus-rstat.sh)"
        np=${np/np: /}
        if [[ ${np} == '[cmus]: -stopped-' ]] ; then
            printf '\n  Cmus is currently playing nothing!\n\n'
            return 3
        fi
        ;;
    esac

    case ${mode} in
    (artist)
        QUERY=${np%% - *}
        ;;
    (album)
        np=${np% - *}
        if [[ ${np} == *' - '* ]] ; then
            QUERY=${np#* - }
        else
            printf '\n Could not get album (%s)!\n\n' "$(cmus-rstat.sh)"
            return 3
        fi
        ;;
    (song)
        QUERY=${np##* - }
        ;;
    esac

    return 0
}

# aliases for lookup backends
lookup -a s=google
lookup -a d=leo
lookup -a we="wikipedia -l en"
lookup -a wd="wikipedia -l de"
